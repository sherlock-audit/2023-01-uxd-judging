koxuan

high

# assets cannot be withdrawn if asset is not whitelisted

## Summary
`UXDController` has a whitelist functionality that controls what assetTokens can be deposited and withdrawn. However, there can be a situation where an asset token is added to whitelist and removed from it later on due to some arbitrary reason. Some of the depositors would have already deposited this asset token in between. As the assets cannot be withdrawn, the last few withdrawers who try to redeem their tokens will not have other whitelisted assets to let them redeem.

## Vulnerability Detail

Owner of UXDController will call whitelistAsset to allow an asset to be deposited in exchange for UXD. 
 
```solidity
    function whitelistAsset(
        address asset,
        bool isWhitelisted
    ) external onlyOwner {
        if (!asset.isContract()) {
            revert CtrlAddressNotContract(asset);
        }
        whitelistedAssets[asset] = isWhitelisted;
        if (isWhitelisted) {
            _addAsset(asset);
        } else {
            _removeAsset(asset);
        }
        emit WhitelistUpdated(
            msg.sender,
            asset,
            isWhitelisted
        );
    }
```
User can call `mint` with the assetToken that the owner just whitelisted.

```solidity
    function mint(
        address assetToken,
        uint256 assetAmount,
        uint256 minAmountOut,
        address receiver
    ) external nonReentrant returns (uint256) {
        // 1. check that token is approved
        // 2. get clearing house from router
        // 3. transfer tokens from msg.sender to clearing house
        // 4. execute perp tx
        // 6. mint
        IERC20 collateral = IERC20(assetToken);
        address account = msg.sender;
        if(collateral.allowance(account, address(this)) < assetAmount) {
            revert CtrlNotApproved(assetToken, account, assetAmount);
        }


        address depository = router.findDepositoryForDeposit(assetToken, assetAmount);
        collateral.transferFrom(
            account,
            depository,
            assetAmount
        );


        InternalMintParams memory mintParams = InternalMintParams({
            assetToken: assetToken,
            assetAmount: assetAmount,
            minAmountOut: minAmountOut,
            receiver: receiver,
            depository: depository
        });
        return _mint(mintParams);
    }
```

Owner now remove asset token from whitelist due to some arbitrary reason by calling`whitelistAsset` with isWhitelisted set to false. Now user tries to withdraw the asset by calling `redeem`, it reverts.

```solidity
    function redeem(
        address assetToken,
        uint256 redeemAmount,
        uint256 minAmountOut,
        address receiver
    ) external nonReentrant returns (uint256) {
        InternalRedeemParams memory rp = InternalRedeemParams({
            assetToken: assetToken,
            amountToRedeem: redeemAmount,
            minAmountOut: minAmountOut,
            intermediary: receiver
        });
        uint256 amountOut = _redeem(rp);
        emit Redeemed(msg.sender, receiver, amountOut);
        return amountOut;
    }
```

In [_redeem](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol#L316-L318),

```solidity
        if (!whitelistedAssets[redeemParams.assetToken]) {
            revert CtrlNotWhitelisted(redeemParams.assetToken);
        }
```

User will call `redeem` with another assetToken that is in the whitelist. However, the asset token that has been removed from whitelist will be stuck in the depository. 





## Impact
Assets that are removed from whitelist after user have deposited will be stuck in the depository, causing the last few withdrawers who withdraw at the end to be unable to redeem their tokens as the assets cannot be withdrawn.

## Code Snippet
[UXDController.sol#L98-L116](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol#L98-L116)
[UXDController.sol#L177-L209](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol#L177-L209)
[UXDController.sol#L264-L279](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol#L264-L279)
[UXDController.sol#L316-L318](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol#L316-L318)
## Tool used

Manual Review

## Recommendation

Recommend allowing withdrawal of assetToken that are not in the whitelist.
