koxuan

high

# rebalance will always revert

## Summary
`rebalance` will close some orders and do a spot swap to rebalance the negative PnL position of PerpDepository. However,   
`spotSwapper`, the uniswap helper contract, is not approved to spend for `PerpDepository`. Hence, `rebalance` will always revert.

## Vulnerability Detail

`spotSwapper` will be set by owner for `rebalance` to do spot swap. It is a helper contract that has a function that wraps uniswap function `exactSwapInput`. 

```solidity
    function setSpotSwapper(address _swapper) external onlyOwner {
        if (!_swapper.isContract()) {
            revert AddressNotContract(_swapper);
        }
        spotSwapper = ISwapper(_swapper);
    }
```
Notice in `rebalance`, there is no `approve` of transfer of baseToken for `spotSwapper` to swap to quoteToken.

```solidity
    function rebalance(
        uint256 amount,
        uint256 amountOutMinimum,
        uint160 sqrtPriceLimitX96,
        uint24 swapPoolFee,
        int8 polarity,
        address account
    ) external nonReentrant returns (uint256, uint256) {
        if (polarity == -1) {
            return
                _rebalanceNegativePnlWithSwap(
                    amount,
                    amountOutMinimum,
                    sqrtPriceLimitX96,
                    swapPoolFee,
                    account
                );
        } else if (polarity == 1) {
            // disable rebalancing positive PnL
            revert PositivePnlRebalanceDisabled(msg.sender);
            // return _rebalancePositivePnlWithSwap(amount, amountOutMinimum, sqrtPriceLimitX96, swapPoolFee, account);
        } else {
            revert InvalidRebalance(polarity);
        }
    }
```

```solidity
    function _rebalanceNegativePnlWithSwap(
        uint256 amount,
        uint256 amountOutMinimum,
        uint160 sqrtPriceLimitX96,
        uint24 swapPoolFee,
        address account
    ) private returns (uint256, uint256) {
        uint256 normalizedAmount = amount.fromDecimalToDecimal(
            ERC20(quoteToken).decimals(),
            18
        );
        _checkNegativePnl(normalizedAmount);
        bool isShort = false;
        bool amountIsInput = true;
        (uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder(
            normalizedAmount,
            isShort,
            amountIsInput,
            sqrtPriceLimitX96
        );
        vault.withdraw(assetToken, baseAmount);
        SwapParams memory params = SwapParams({
            tokenIn: assetToken,
            tokenOut: quoteToken,
            amountIn: baseAmount,
            amountOutMinimum: amountOutMinimum,
            sqrtPriceLimitX96: sqrtPriceLimitX96,
            poolFee: swapPoolFee
        });
        uint256 quoteAmountOut = spotSwapper.swapExactInput(params);
        int256 shortFall = int256(
            quoteAmount.fromDecimalToDecimal(18, ERC20(quoteToken).decimals())
        ) - int256(quoteAmountOut);
        if (shortFall > 0) {
            IERC20(quoteToken).transferFrom(
                account,
                address(this),
                uint256(shortFall)
            );
        } else if (shortFall < 0) {
            // we got excess tokens in the spot swap. Send them to the account paying for rebalance
            IERC20(quoteToken).transfer(
                account,
                _abs(shortFall)
            );
        }
        vault.deposit(quoteToken, quoteAmount);


        emit Rebalanced(baseAmount, quoteAmount, shortFall);
        return (baseAmount, quoteAmount);
    }
```

This is problematic as in  `uint256 quoteAmountOut = spotSwapper.swapExactInput(params);`, swapExactInput does a safeTransferFrom baseToken from `PerpDepository` to `spotSwapper`.

```solidity
    function swapExactInput(SwapParams memory params) external returns (uint256 amountOut) {
        // msg.sender must approve this contract


        // Transfer the specified amount of tokenIn to this contract.
        TransferHelper.safeTransferFrom(params.tokenIn, msg.sender, address(this), params.amountIn);


        // Approve the router to spend tokenIn.
        TransferHelper.safeApprove(params.tokenIn, address(swapRouter), params.amountIn);


        ISwapRouter.ExactInputSingleParams memory uniswapParams =
            ISwapRouter.ExactInputSingleParams({
                tokenIn: params.tokenIn,
                tokenOut: params.tokenOut,
                fee: params.poolFee,
                recipient: msg.sender,
                deadline: block.timestamp,
                amountIn: params.amountIn,
                amountOutMinimum: params.amountOutMinimum,
                sqrtPriceLimitX96: params.sqrtPriceLimitX96
            });


        amountOut = swapRouter.exactInputSingle(uniswapParams);
    }
```

```solidity
    function safeTransferFrom(
        address token,
        address from,
        address to,
        uint256 value
    ) internal {
        (bool success, bytes memory data) =
            token.call(abi.encodeWithSelector(IERC20.transferFrom.selector, from, to, value));

        require(success && (data.length == 0 || abi.decode(data, (bool))), 'STF');

        console.log("hello");
    }

```



## Impact
`perpDepository` can never be rebalance with swap, causing protocol to fail as it cannot achieve delta neutral position unless someone rebalanceLite by providing large amounts of token to rebalance. 


## Code Snippet
[PerpDepository.sol#L154-L159](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L154-L159)
[PerpDepository.sol#L446-L470](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L446-L470)
[PerpDepository.sol#L478-L528](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L478-L528)
[Uniswapper.sol#L24-L46](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/uniswap/Uniswapper.sol#L24-L46)
[TransferHelper.sol#L13-L22](https://github.com/Uniswap/v3-periphery/blob/main/contracts/libraries/TransferHelper.sol#L13-L22)


## Tool used

Manual Review

## Recommendation

Recommend approving `baseAmount` for spotSwapper before doing the swap.

```solidity
baseToken.approve(spotSwapper, baseAmount);
```
