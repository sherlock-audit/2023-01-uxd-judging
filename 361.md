serial-coder

medium

# [M-04] Unwhitelisting Collateral Tokens Affect PERP Positions

## Summary

The `UXDController.whitelistAsset()` function allows an owner to unwhitelist asset tokens, making them unable to be used as collateral tokens for minting or redeeming the `UXD` token.

I discovered that if collateral tokens are unwhitelisted, all opening PERP positions (on both `Perpetual Protocol` and `Rage Trade`) using the unwhitelisted tokens as collaterals would no longer be closed. Consequently, the PERP positions could be forced to be liquidated if the values of the collateral assets are lower than those of the loan positions or reach the liquidation points.

## Vulnerability Detail

In the UXD protocol, an owner can unwhitelist asset tokens from being used as collateral assets for minting or redeeming the `UXD` token via the `UXDController.whitelistAsset()` function (L105 and L109 in Snippet 1).

```solidity
SNIPPET: 1
FILE: https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol
LOCATIONS: L105 and L109

98:     function whitelistAsset(
99:         address asset,
100:        bool isWhitelisted
101:    ) external onlyOwner {
102:        if (!asset.isContract()) {
103:            revert CtrlAddressNotContract(asset);
104:        }
105: *      whitelistedAssets[asset] = isWhitelisted;
106:        if (isWhitelisted) {
107:            _addAsset(asset);
108:        } else {
109: *          _removeAsset(asset);
110:        }
111:        emit WhitelistUpdated(
112:            msg.sender,
113:            asset,
114:            isWhitelisted
115:        );
116:    }
```

During the `UXD` redemption process, the internal function `UXDController._redeem()` would be executed. The function would verify that a target asset token (collateral) for which a user would like to redeem the `UXD` tokens is in the whitelist (L316 - 318 in Snippet 2).

In other words, the function would revert the user's redemption transaction if the target collateral asset is unwhitelisted (L317).

```solidity
SNIPPET: 2
FILE: https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol
LOCATIONS: L316 - 318

312:    function _redeem(InternalRedeemParams memory redeemParams)
313:        internal
314:        returns (uint256)
315:    {
316: *      if (!whitelistedAssets[redeemParams.assetToken]) {
317: *          revert CtrlNotWhitelisted(redeemParams.assetToken);
318: *      }
319:        if(redeemable.allowance(msg.sender, address(this)) < redeemParams.amountToRedeem) {
320:            revert CtrlNotApproved(address(redeemable), msg.sender, redeemParams.amountToRedeem);
321:        }
322:        
323:        address depository = router.findDepositoryForRedeem(
324:            redeemParams.assetToken,
325:            redeemParams.amountToRedeem
326:        );
327:
328:        uint256 amountOut = IDepository(depository).redeem(
329:            redeemParams.assetToken, 
330:            redeemParams.amountToRedeem
331:        );
332:
333:        if (amountOut < redeemParams.minAmountOut) {
334:            revert CtrlMinNotMet(redeemParams.minAmountOut, amountOut);
335:        }
336:        redeemable.burn(msg.sender, redeemParams.amountToRedeem);
337:        IERC20(redeemParams.assetToken).transfer(redeemParams.intermediary, amountOut);
338:
339:        return amountOut;
340:    }
```

Let's examine the `PerpDepository` contract, the manager contract interacting with the external `Perp-Curie` contract (of the `Perpetual Protocol`) and holding all opening PERP positions. There can be several `PerpDepository` contracts in the UXD protocol. Each `PerpDepository` contract manages all PERP positions opening with a particular underlying collateral asset.

Snippet 3 below shows the `PerpDepository.redeem()` function. The function would accept redeeming the `UXD` tokens for a specific collateral asset (L268 - 271). This means that if a particular collateral asset managed by a specific `PerpDepository` contract is unwhitelisted, all opening PERP positions would not be able to be closed.

```solidity
SNIPPET: 3
FILE: https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol
LOCATIONS: L268 - 271

264:    function redeem(
265:        address asset,
266:        uint256 amount
267:    ) external onlyController returns (uint256) {
268: *      if (asset == assetToken) {
269: *          (uint256 base, ) = _openLong(amount);
270: *          _withdrawAsset(base, address(controller));
271: *          return base;
272:        } else if (asset == quoteToken) {
273:            revert QuoteRedeemDisabled(msg.sender);
274:            // return _processQuoteRedeem(amount);
275:        } else {
276:            revert UnsupportedAsset(asset);
277:        }
278:    }
```

Like the `PerpDepository` contract,  each `RageDnDepository` contract is a manager contract interacting with the external `Rage Trade` contract and holding all positions opening with a particular underlying collateral asset.

Snippet 4 presents the `RageDnDepository.redeem()` function. The function accepts redeeming the `UXD` tokens for a specific collateral asset (L125 - 127). Thus, all opening positions would not be able to be closed if their collateral asset is unwhitelisted.

```solidity
SNIPPET: 4
FILE: https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/rage-trade/RageDnDepository.sol
LOCATIONS: L125 - 127

120:    function redeem(address asset, uint256 redeemableAmount)
121:        external
122:        onlyController
123:        returns (uint256)
124:    {
125: *      if (asset != assetToken) {
126: *          revert UnsupportedAsset(asset);
127: *      }
128:        uint256 assetAmount = _redeemableToAssets(redeemableAmount);
129:        redeemableUnderManagement -= redeemableAmount;
130:        netAssetDeposits -= assetAmount;
131:        uint256 shares = vault.withdraw(
132:            assetAmount,
133:            address(controller),
134:            address(this)
135:        );
136:        emit Withdrawn(msg.sender, assetAmount, redeemableAmount, shares);
137:        return assetAmount;
138:    }
```

## Impact

If asset tokens are unwhitelisted, all PERP positions (opening on both `Perpetual Protocol` and `Rage Trade`) using the unwhitelisted tokens as collaterals would be frozen. Specifically, users would be unable to close the frozen positions (using the `UXD` redemption process). Consequently, the PERP positions could be forced to be liquidated if the values of the collateral assets are lower than those of the loan positions or reach the liquidation points (impact is HIGH).

For the likelihood of this issue, I consider LOW (but it can also be MEDIUM) based on the administration of the protocol. Hence, I evaluate the risk of this issue to MEDIUM.

## Code Snippet

https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol#L105

https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol#L109

https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol#L316-L318

https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L268-L271

https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/rage-trade/RageDnDepository.sol#L125-L127

## Tool used

Manual Review

## Recommendation

Consider applying appropriate solutions to ensure that the PERP positions will not be frozen. One possible solution is to remove the **`whitelisted asset check`** from the `UXDController._redeem()` function (L316 - 318 in the below snippet).

This ensures that the unwhitelisted assets would not be used for opening the PERP positions but guarantees that the already opened positions can be closed.

```solidity
SNIPPET: 5
FILE: https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol
LOCATIONS: L316 - 318

312:    function _redeem(InternalRedeemParams memory redeemParams)
313:        internal
314:        returns (uint256)
315:    {
316: *      // if (!whitelistedAssets[redeemParams.assetToken]) {
317: *      //     revert CtrlNotWhitelisted(redeemParams.assetToken);
318: *      // }
319:        if(redeemable.allowance(msg.sender, address(this)) < redeemParams.amountToRedeem) {
320:            revert CtrlNotApproved(address(redeemable), msg.sender, redeemParams.amountToRedeem);
321:        }
322:        
323:        address depository = router.findDepositoryForRedeem(
324:            redeemParams.assetToken,
325:            redeemParams.amountToRedeem
326:        );
327:
328:        uint256 amountOut = IDepository(depository).redeem(
329:            redeemParams.assetToken, 
330:            redeemParams.amountToRedeem
331:        );
332:
333:        if (amountOut < redeemParams.minAmountOut) {
334:            revert CtrlMinNotMet(redeemParams.minAmountOut, amountOut);
335:        }
336:        redeemable.burn(msg.sender, redeemParams.amountToRedeem);
337:        IERC20(redeemParams.assetToken).transfer(redeemParams.intermediary, amountOut);
338:
339:        return amountOut;
340:    }
```