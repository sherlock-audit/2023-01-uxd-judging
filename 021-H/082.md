ctf_sec

high

# withdrawInsurance can revert unexpectedly because of the free collateral utilization rate and the funding rate on Perp protocol

## Summary

withdrawInsurance can revert unexpectedly because of the free collateral utilization rate and the funding rate on Perp protocol

## Vulnerability Detail

In the current implementation of perpDepository, we have two functions:

depositInsurane and withdrawInsurance

```solidity
function depositInsurance(uint256 amount, address from)
	external
	nonReentrant
	onlyOwner
{
	if (amount == 0) {
		revert ZeroAmount();
	}
	uint256 allowance = IERC20(insuranceToken()).allowance(
		from,
		address(this)
	);
	if (allowance < amount) {
		revert NotApproved(allowance, amount);
	}

	insuranceDeposited += amount;

	IERC20(insuranceToken()).transferFrom(from, address(this), amount);
	IERC20(insuranceToken()).approve(address(vault), amount);
	vault.deposit(insuranceToken(), amount);

	emit InsuranceDeposited(msg.sender, from, amount);
}
```

and

```solidity
/// @notice Withdraws insurance from this depository
/// @dev Can only be called by Owner (governance).
/// @param amount The amount to withdraw.
/// @param to The account to withdraw to.
function withdrawInsurance(uint256 amount, address to)
	external
	nonReentrant
	onlyOwner
{
	if (amount == 0) {
		revert ZeroAmount();
	}

	insuranceDeposited -= amount;

	vault.withdraw(insuranceToken(), amount);
	IERC20(insuranceToken()).transfer(to, amount);

	emit InsuranceWithdrawn(msg.sender, to, amount);
}
```

The issue is that the code use insuranceDeposited to track how much fund is deposited and even how much fund can be withdrawal.

```solidity
insuranceDeposited -= amount;

vault.withdraw(insuranceToken(), amount);
IERC20(insuranceToken()).transfer(to, amount);
```

However, withdrawInsurance can revert unexpectedly because of the free collateral utilization rate and the funding rate on Perp protocol

According to 

https://support.perp.com/hc/en-us/articles/7917807368729-Perp-v2-Integration-Guide-Code-Samples#heading-6

> When withdrawing collaterals, one can withdraw the amount up to your freeCollateral. This makes sure that positions are always sufficiently collateralized.

> function getFreeCollateral(address trader) external view returns (uint256);

The aount of fund can be withdraw is not determined by insuranceDeposited but it is determined how many free collateral left.

Because when mint with assetToken, short position is opened, which exposes the depository to market price volatilty, the available free collateral can be out of sycn with the insuranceDeposited. If the market price drops, the number of freeCollateral decreases.

The amount that can be withdraw is also determined by funding rate, when withdraw happens on vault, funding rate applies. These can increase the balance of trader or slash the balance of trader.

https://github.com/perpetual-protocol/perp-curie-contract/blob/27ea8e2c4be37d1dd58c1eed3b3cc269d398a091/contracts/Vault.sol#L187

```solidity
function withdraw(address token, uint256 amount)
	external
	override
	whenNotPaused
	nonReentrant
	onlySettlementOrCollateralToken(token)
{
	// input requirement checks:
	//   token: here
	//   amount: in _settleAndDecreaseBalance()

	address to = _msgSender();
	_withdraw(to, token, amount);
}
```

Which calls:

https://github.com/perpetual-protocol/perp-curie-contract/blob/27ea8e2c4be37d1dd58c1eed3b3cc269d398a091/contracts/Vault.sol#L689

```solidity
function _withdraw(
	address to,
	address token,
	uint256 amount
) internal {
	_settleAndDecreaseBalance(to, token, amount);
	SafeERC20Upgradeable.safeTransfer(IERC20Upgradeable(token), to, amount);
	emit Withdrawn(token, to, amount);
}
```

which calls _settleAndDecreaseBalance(to, token, amount);

https://github.com/perpetual-protocol/perp-curie-contract/blob/27ea8e2c4be37d1dd58c1eed3b3cc269d398a091/contracts/Vault.sol#L665

```solidity
function _settleAndDecreaseBalance(
	address to,
	address token,
	uint256 amount
) internal {
	// settle all funding payments owedRealizedPnl
	// pending fee can be withdraw but won't be settled
	IClearingHouse(_clearingHouse).settleAllFunding(to);

	// incl. owedRealizedPnl
	uint256 freeCollateral = getFreeCollateralByToken(to, token);
	// V_NEFC: not enough freeCollateral
	require(freeCollateral >= amount, "V_NEFC");

	int256 deltaBalance = amount.toInt256().neg256();
	if (token == _settlementToken) {
		// settle both the withdrawn amount and owedRealizedPnl to collateral
		int256 owedRealizedPnlX10_18 = IAccountBalance(_accountBalance).settleOwedRealizedPnl(to);
		deltaBalance = deltaBalance.add(owedRealizedPnlX10_18.formatSettlementToken(_decimals));
	}

	_modifyBalance(to, token, deltaBalance);
}
```

## Impact

When the free collateral becomes less when the market price falls, the amount can be withdraw is less than insuranceDeposited.

To impact of funding rate is outlined below

https://support.perp.com/hc/en-us/articles/5257580412569

Funding rate can slash the trader's balance or increase the trader's balance.

In the case when the trader's balance is slashed, the insuranceDeposited number can be 100 ETH, but 1 ETH balance is slashed and paid for funding rate. 

The amount of withdrawallable insurance funds goes to 99 ETH, which is less than
insuranceDeposited 

In the case when trader's position earns funding payment,

the  insuranceDeposited is 100 ETH, and the trader's positoin receives 1 ETH of funding payment, the amount of withdrawallable insurance funds goes to 101 ETH, but the owner governance cannot withdraw the full 101 ETH balance because trying to withdraw 101 ETH will trigger the arithmic underflow because the insuranceDeposited is 100 ETH.

```solidity
insuranceDeposited -= amount;
```

## Code Snippet

https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L172-L224

## Tool used

Manual Review

## Recommendation

We recommend use 

```solidity
function getFreeCollateral(address trader) external view returns (uint256);
```

to check what is the available balance to withdraw instead of using insuranceDeposited variable.