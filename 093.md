ctf_sec

high

# Lack of input validation when rebalancing occurs, which leads to loss of fund in Perp protocol depository

## Summary

Lack of input validation when rebalancing occurs, which leads to loss of fund in Perp protocol depository

## Vulnerability Detail

The rebalance logic is crucial to make sure the UXD token is backed by delta-neural position.

In the current implementation, the positive rebalance is disabled, but if the PNL goes negative, 

rebalance can be triggered. the function lacks access control and anyone can call the function when the depository is in the state of negative PNL.

```solidity
function rebalance(
	uint256 amount,
	uint256 amountOutMinimum,
	uint160 sqrtPriceLimitX96,
	uint24 swapPoolFee,
	int8 polarity,
	address account
) external nonReentrant returns (uint256, uint256) {
	if (polarity == -1) {
		return
			_rebalanceNegativePnlWithSwap(
				amount,
				amountOutMinimum,
				sqrtPriceLimitX96,
				swapPoolFee,
				account
			);
	} else if (polarity == 1) {
		// disable rebalancing positive PnL
		revert PositivePnlRebalanceDisabled(msg.sender);
		// return _rebalancePositivePnlWithSwap(amount, amountOutMinimum, sqrtPriceLimitX96, swapPoolFee, account);
	} else {
		revert InvalidRebalance(polarity);
	}
}
```

which calls:

```solidity
 // Collateral price has increased, thus, positionValue > supply. close part of open position
    // close (amount quote) part of position => (baseAmount, quoteAmount)
    // withdraw baseAmount base token.
    // swap base => quote
    // Transfer shortfall from swap from `account`
    // deposit USDC to cover negative PnL
    function _rebalanceNegativePnlWithSwap(
        uint256 amount,
        uint256 amountOutMinimum,
        uint160 sqrtPriceLimitX96,
        uint24 swapPoolFee,
        address account
    ) private returns (uint256, uint256) {
        uint256 normalizedAmount = amount.fromDecimalToDecimal(
            ERC20(quoteToken).decimals(),
            18
        );
        _checkNegativePnl(normalizedAmount);
        bool isShort = false;
        bool amountIsInput = true;
        (uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder(
            normalizedAmount,
            isShort,
            amountIsInput,
            sqrtPriceLimitX96
        );
        vault.withdraw(assetToken, baseAmount);
        SwapParams memory params = SwapParams({
            tokenIn: assetToken,
            tokenOut: quoteToken,
            amountIn: baseAmount,
            amountOutMinimum: amountOutMinimum,
            sqrtPriceLimitX96: sqrtPriceLimitX96,
            poolFee: swapPoolFee
        });
        uint256 quoteAmountOut = spotSwapper.swapExactInput(params);
        int256 shortFall = int256(
            quoteAmount.fromDecimalToDecimal(18, ERC20(quoteToken).decimals())
        ) - int256(quoteAmountOut);
        if (shortFall > 0) {
            IERC20(quoteToken).transferFrom(
                account,
                address(this),
                uint256(shortFall)
            );
        } else if (shortFall < 0) {
            // we got excess tokens in the spot swap. Send them to the account paying for rebalance
            IERC20(quoteToken).transfer(
                account,
                _abs(shortFall)
            );
        }
        vault.deposit(quoteToken, quoteAmount);

        emit Rebalanced(baseAmount, quoteAmount, shortFall);
        return (baseAmount, quoteAmount);
    }
```

note the input parameter:

```solidity
function _rebalanceNegativePnlWithSwap(
	uint256 amount,
	uint256 amountOutMinimum,
	uint160 sqrtPriceLimitX96,
	uint24 swapPoolFee,
	address account
)
```

the parameter is amount has some degree of validation to make sure over-rebalance do not happen:

```solidity
uint256 normalizedAmount = amount.fromDecimalToDecimal(
	ERC20(quoteToken).decimals(),
	18
);
_checkNegativePnl(normalizedAmount);
```

However, the parameter amountOutMinimum, swapPoolFee and account has no validtion.

## Impact

the code is expect be swap WETH collateral for USDC. 

For example, there is pool A in Uniswap V3 that charges 0.05% with thick and centralized liquidity.

The caller of the rebalance function can create a WETH / USDC pool in Uniswap V3 that charge very large fee, more than 0.3%, or 1% of the fee with thin liquidity.

The swap that go through the thin liquidity pool with high swap fee can make the protocol lose fund at the fee paid during swap.

The parameter account has no validation as well, this is very problematic:

```solidity
uint256 quoteAmountOut = spotSwapper.swapExactInput(params);
int256 shortFall = int256(
	quoteAmount.fromDecimalToDecimal(18, ERC20(quoteToken).decimals())
) - int256(quoteAmountOut);
if (shortFall > 0) {
	IERC20(quoteToken).transferFrom(
		account,
		address(this),
		uint256(shortFall)
	);
} else if (shortFall < 0) {
	// we got excess tokens in the spot swap. Send them to the account paying for rebalance
	IERC20(quoteToken).transfer(
		account,
		_abs(shortFall)
	);
}
```

If the shortFall happens, it is the account that pays for the shortFall,

```solidity
if (shortFall > 0) {
	IERC20(quoteToken).transferFrom(
		account,
		address(this),
		uint256(shortFall)
	);
}
```

Account A can approve the depository to transfer quote token out of Account A's wallet,

but Account B can call this function and transfer the fund out of account A and pays for the shortfall and pays for negative PNL, This clearly leads to loss of fund.

Who wants to give the depository approval to transfer the token? the depositor of the perp depository insurance fund can be one address because the smart contract need the approval to transfer the quote token then depository into perp protocol's vault as free collateral.

```solidity
/// @notice Deposits insurance to this depository
/// @dev Insurance token is set at contract initialization.
/// Only called by the owner
/// @param amount the amount to deposit
/// @param from the account to deposit from. This account must have approved this contract to
// spend >= amount.
function depositInsurance(uint256 amount, address from)
	external
	nonReentrant
	onlyOwner
{
	if (amount == 0) {
		revert ZeroAmount();
	}
	uint256 allowance = IERC20(insuranceToken()).allowance(
		from,
		address(this)
	);
	if (allowance < amount) {
		revert NotApproved(allowance, amount);
	}

	insuranceDeposited += amount;

	IERC20(insuranceToken()).transferFrom(from, address(this), amount);
	IERC20(insuranceToken()).approve(address(vault), amount);
	vault.deposit(insuranceToken(), amount);

	emit InsuranceDeposited(msg.sender, from, amount);
}
```

note the address from needs to given the smart contract approval first, otherwise the code below will not executes.

```solidity
IERC20(insuranceToken()).transferFrom(from, address(this), amount);
```

If the address from that pays for insurance fund give unlimited approval and the wallet owenr is not aware of the lack of access in rebalance smart contract, another account can create a pool with thin liquidity, charge high fee and let the address "from" that pays for insurance fund pais for the short fall.

```solidity
IERC20(quoteToken).transferFrom(
	account,
	address(this),
	uint256(shortFall)
);
```

In the current implementation, 

the quoteToken match the insuranceToken() as well.

```solidity
function insuranceToken() public view returns (address) {
	return quoteToken;
}
```

Again, if account A give approval to let the smart contract transfer the fund out of the account A's wallet, account B should not call the smart contract to transfer the fund out of account A's wallet, otherwise, a malicious account B can drain the fund in the account A.

## Code Snippet

https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L436-L470

https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L471-L529

## Tool used

Manual Review

## Recommendation

We recommend the project change from to msg.sender in rebalanceNegativePNL.

Change from

```solidity
IERC20(quoteToken).transferFrom(
	account,
	address(this),
	uint256(shortFall)
);
```

to

```solidity
IERC20(quoteToken).transferFrom(
	msg.sender,
	address(this),
	uint256(shortFall)
);
```