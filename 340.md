berndartmueller

high

# Rebalancing a negative Perp PnL will fail to deposit to the vault due to decimal precision inconsistencies

## Summary

The Perp vault operates (see [token transfer logic](https://github.com/perpetual-protocol/perp-curie-contract/blob/main/contracts/Vault.sol#L607)) with the underlying token decimal precision (`WETH` 18 decimals, `USDC` - 6 decimals). The clearing house expects all amounts to be in 18 decimal precision when opening (short, long) positions. Failing to use the correct decimal precision when depositing to the vault results in an overinflated amount of `quoteToken` deposited to the vault or a revert due to insufficient balance.

## Vulnerability Detail

The Perp protocol uses a vAMM with virtual tokens (e.g. `vBTC`, `vWETH`, `vUSD`). `vUSD` is the quote token for all pairs. All virtual tokens share the same decimal precision (**18 decimals**). Opening positions expects all amounts passed as parameters to be in 18 decimal precision.

Depositing collateral - `WETH` typically as `assetToken` and `USDC` as `quoteToken`, works with the decimal precision of the token itself (`WETH` has 18 decimals, `USDC` has 6 decimals). The `PerpDepository` contract is aware of the different decimal precisions and converts (normalizes) the amounts to 18 decimals when opening positions with the Perp `clearingHouse`.

However, in the `PerpDepository._rebalanceNegativePnlWithSwap` function, the deposited `quoteAmount` is in 18 decimal precision and not converted to the required decimals by the underlying `quoteToken` (`USDC`). Please note that in L509, the `quoteAmount` is converted to `ERC20(quoteToken).decimals()` to calculate the shortfall but not used as the vault deposit amount.

## Impact

Rebalancing a negative Perp PnL will deposit an overinflated amount of `quoteToken` to the vault or revert due to insufficient balance (due to the overinflated amount).

## Code Snippet

[integrations/perp/PerpDepository.sol#L524](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L524)

```solidity
// Collateral price has increased, thus, positionValue > supply. close part of open position
// close (amount quote) part of position => (baseAmount, quoteAmount)
// withdraw baseAmount base token.
// swap base => quote
// Transfer shortfall from swap from `account`
// deposit USDC to cover negative PnL
function _rebalanceNegativePnlWithSwap(
    uint256 amount,
    uint256 amountOutMinimum,
    uint160 sqrtPriceLimitX96,
    uint24 swapPoolFee,
    address account
) private returns (uint256, uint256) {
    uint256 normalizedAmount = amount.fromDecimalToDecimal(
        ERC20(quoteToken).decimals(),
        18
    );
    _checkNegativePnl(normalizedAmount);
    bool isShort = false;
    bool amountIsInput = true;
    (uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder( // @audit-info `quoteAmount` is in 18 decimal precision
        normalizedAmount,
        isShort,
        amountIsInput,
        sqrtPriceLimitX96
    );
    vault.withdraw(assetToken, baseAmount);
    SwapParams memory params = SwapParams({
        tokenIn: assetToken,
        tokenOut: quoteToken,
        amountIn: baseAmount,
        amountOutMinimum: amountOutMinimum,
        sqrtPriceLimitX96: sqrtPriceLimitX96,
        poolFee: swapPoolFee
    });
    uint256 quoteAmountOut = spotSwapper.swapExactInput(params);
    int256 shortFall = int256(
        quoteAmount.fromDecimalToDecimal(18, ERC20(quoteToken).decimals())
    ) - int256(quoteAmountOut);
    if (shortFall > 0) {
        IERC20(quoteToken).transferFrom(
            account,
            address(this),
            uint256(shortFall)
        );
    } else if (shortFall < 0) {
        // we got excess tokens in the spot swap. Send them to the account paying for rebalance
        IERC20(quoteToken).transfer(
            account,
            _abs(shortFall)
        );
    }
    vault.deposit(quoteToken, quoteAmount); // @audit-info `quoteToken` (i.e. USDC) has 6 decimals, `quoteAmount` is 18 decimals -> overinflated deposit amount or reverts

    emit Rebalanced(baseAmount, quoteAmount, shortFall);
    return (baseAmount, quoteAmount);
}
```

## Tool used

Manual Review

## Recommendation

Consider converting `quoteAmount` (18 decimals) to the same decimal precision as `quoteToken` (USDC - 6 decimals) before depositing it to the vault (same as already done in [L509](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L509)).
