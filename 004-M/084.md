ctf_sec

medium

# Lack of slippage protection when modifying position with perp protocol depository

## Summary

Lack of slippage protection when modifying position with perp protocol depository

## Vulnerability Detail

If we look into the PerpDepository.sol, in open long and open short code

```solidity
/// @notice Opens a long position on the perpetual DEX.
/// @dev This closes a portion of the previously open short backing the delta-neutral position.
/// Only called by the controller
/// @param amount The amount to open long position for.
/// `isBaseToQuote == false`, `exactInput == true`, so this is the quote amount.
function _openLong(uint256 amount)
	private
	returns (uint256, uint256)
{
	(uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder(
		amount,
		false, // isShort
		true, // isExactInput
		0 // sqrtPriceLimitX96
	);
	redeemableUnderManagement -= quoteAmount;

	return (baseAmount, quoteAmount);
}

/// @notice Opens a short position on the perpetual DEX.
/// @dev This increases the size of the delta-neutral position.
/// Can only be called by the controller
/// @param amount The amount of short position to open. THis is opened with `exactInput = true`,
/// thus, this is the input/base token amount.
/// @return base, quote
function _openShort(uint256 amount)
	private
	returns (uint256, uint256)
{
	(uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder(
		amount,
		true, // short
		true, // exactInput
		0
	);
	redeemableUnderManagement += quoteAmount;
	_checkSoftCap();
	// emit event here
	return (baseAmount, quoteAmount);
}
```

the parameter sqrtPriceLimitX96 is set to 0,

Then the function _placePerpOrder is called:

```solidity
function _placePerpOrder(
	uint256 amount,
	bool isShort,
	bool amountIsInput,
	uint160 sqrtPriceLimit
) private returns (uint256, uint256) {
	uint256 upperBound = 0; // 0 = no limit, limit set by sqrtPriceLimit

	IClearingHouse.OpenPositionParams memory params = IClearingHouse
		.OpenPositionParams({
			baseToken: market,
			isBaseToQuote: isShort, // true for short
			isExactInput: amountIsInput, // we specify exact input amount
			amount: amount, // collateral amount - fees
			oppositeAmountBound: upperBound, // output upper bound
			// solhint-disable-next-line not-rely-on-time
			deadline: block.timestamp,
			sqrtPriceLimitX96: sqrtPriceLimit, // max slippage
			referralCode: 0x0
		});

	(uint256 baseAmount, uint256 quoteAmount) = clearingHouse.openPosition(
		params
	);
	uint256 feeAmount = _calculatePerpOrderFeeAmount(quoteAmount);
	totalFeesPaid += feeAmount;

	emit PositionOpened(isShort, amount, amountIsInput, sqrtPriceLimit);
	return (baseAmount, quoteAmount);
}
```

as we can see in the comment:

```solidity
uint256 upperBound = 0; // 0 = no limit, limit set by sqrtPriceLimit
```

but sqrtPriceLimit is always set to 0.

and upperBound is also set to 0.

## Impact

Both upperBound and sqrtPriceLimit is set to 0 and the slippage protection is disabled. Then the position opened can suffer from slippage loss.

According to the on-chain context:

```solidity
DEPLOYMENT: mainnet, arbitrum, optimism
ERC20: USDC, WETH
ERC721: none
ERC777: none
FEE-ON-TRANSFER: none
REBASING TOKENS: none
ADMIN: trusted
```

If the protocol is deployed in ethereum mainnet, frontrunning can be a issue.

https://github.com/perpetual-protocol/perp-curie-contract/blob/27ea8e2c4be37d1dd58c1eed3b3cc269d398a091/contracts/interface/IClearingHouse.sol#L48

```solidity
/// @param oppositeAmountBound
// B2Q + exact input, want more output quote as possible, so we set a lower bound of output quote
// B2Q + exact output, want less input base as possible, so we set a upper bound of input base
// Q2B + exact input, want more output base as possible, so we set a lower bound of output base
// Q2B + exact output, want less input quote as possible, so we set a upper bound of input quote
// when it's set to 0, it will disable slippage protection entirely regardless of exact input or output
// when it's over or under the bound, it will be reverted
/// @param sqrtPriceLimitX96
// B2Q: the price cannot be less than this value after the swap
// Q2B: the price cannot be greater than this value after the swap
// it will fill the trade until it reaches the price limit but WON'T REVERT
// when it's set to 0, it will disable price limit;
// when it's 0 and exact output, the output amount is required to be identical to the param amount
struct OpenPositionParams {
	address baseToken;
	bool isBaseToQuote;
	bool isExactInput;
	uint256 amount;
	uint256 oppositeAmountBound;
	uint256 deadline;
	uint160 sqrtPriceLimitX96;
	bytes32 referralCode;
}
```

## Code Snippet

https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L303-L376

## Tool used

Manual Review

## Recommendation

We recommend the project enable the slippage protection when opening long or short position.

