yixxas

high

# Perpetual orders can be sandwiched/frontrun due to 0 slippage set

## Summary
Slippage is set to 0 when we place a perpetual order in both `openLong()` and `openShort()`. This is incredibly dangerous as every order made by the protocol can be frontrun to extract maximmum value from it.

## Vulnerability Detail
In `_placePerpOrder()`, we see

>   uint256 upperBound = 0; // 0 = no limit, limit set by sqrtPriceLimit

`upperBound` is set to 0, and it claims that limit is being set by `sqrtPriceLimit`.

```solidity
    function _placePerpOrder(
        uint256 amount,
        bool isShort,
        bool amountIsInput,
        uint160 sqrtPriceLimit
    ) private returns (uint256, uint256) {
        uint256 upperBound = 0; // 0 = no limit, limit set by sqrtPriceLimit

        IClearingHouse.OpenPositionParams memory params = IClearingHouse
            .OpenPositionParams({
                baseToken: market,
                isBaseToQuote: isShort, // true for short
                isExactInput: amountIsInput, // we specify exact input amount
                amount: amount, // collateral amount - fees
                oppositeAmountBound: upperBound, // output upper bound
                // solhint-disable-next-line not-rely-on-time
                deadline: block.timestamp,
                sqrtPriceLimitX96: sqrtPriceLimit, // max slippage
                referralCode: 0x0
            });
		...
    }
```

However, we can see in both `openLong()` and `openShort()` that the inputs for used for `sqrtPriceLimit` are both 0.
```solidity
    function _openLong(uint256 amount)
        private
        returns (uint256, uint256)
    {
        (uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder(
            amount,
            false, // isShort
            true, // isExactInput
            0 // sqrtPriceLimitX96
        );
        redeemableUnderManagement -= quoteAmount;

        return (baseAmount, quoteAmount);
    }

    function _openShort(uint256 amount)
        private
        returns (uint256, uint256)
    {
        (uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder(
            amount,
            true, // short
            true, // exactInput
            0
        );
        redeemableUnderManagement += quoteAmount;
        _checkSoftCap();
        // emit event here
        return (baseAmount, quoteAmount);
    }
```

Furthermore, we are using `exactInput`. We have no control over the `baseAmount` or `quoteAmount` we are receiving.


## Impact

Orders are susceptible to being sandwiched or frontrun, incuring huge losses to users of the protocol. For example, when a user `redeem()` UXD tokens, they will get back a much lesser than expected amount of `assetToken` or `quoteToken` due to the lack of slippage protection.

## Code Snippet
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L352
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L309-L322
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L330-L344

## Tool used

Manual Review

## Recommendation
We should set slippage protection with `oppositeAmountBound`.

