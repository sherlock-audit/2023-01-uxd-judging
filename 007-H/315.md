DecorativePineapple

medium

# The periodSize of the oracle is very low allowing the TWAP price to be easily manipulated

## Summary
The `currentSize` of the short position is calculated by multiplying the `markPrice` with the `positionSize`. However, the `markPrice` is calculated by fetching the TWAP from a Uniswapv3 pair with a small periodSize, meaning that the an attacker can easily manipulate the TWAP and force the `PerpDepository` smart contract to rebalance even when the PnL is not negative.

## Vulnerability Detail
When the `rebalanceLite` and the `rebalance` functions are called it's checked that whether negative Pnl exists. The unrealized Pnl from the delta neutral position is calculated by subtracting the current size of the short position from the `redeemableUnderManagement`. In order to calculate the current size of the short position the `markPrice` is fetched from the `getSqrtMarkTwapX96` function of the exchange contract.
As we can see in the [`Exchange.sol:0xe62e90efc179a36ca80a9e9f2a64e02fc6066e00`](https://optimistic.etherscan.deth.net/address/0xe62e90efc179a36ca80a9e9f2a64e02fc6066e00#code) the `getSqrtMarkTwapX96` function calls the `observe` function from the Uniswapv3 pool in order to calculate the `time weighted average tick`. However, the 15 seconds which are given as argument are very small, meaning that the TWAP can be manipulated with low funds. An attacker can manipulate the TWAP and force the `PerpDepository` smart contract to rebalance even when the PnL is not negative.

For reference about Uniswap V3 risks:
- [https://docs.euler.finance/risk-framework/oracle-rating  To](https://docs.euler.finance/risk-framework/oracle-rating%E2%80%A8%E2%80%A8To) compute estimates of TWAP manipulations:
- https://oracle.euler.finance/

The `getSqrtMarkTwapX96()` and the `getSqrtMarkTwapX96()` functions:
```solidity
 function getSqrtMarkTwapX96(address baseToken, uint32 twapInterval) public view override returns (uint160) {
        return UniswapV3Broker.getSqrtMarkTwapX96(IMarketRegistry(_marketRegistry).getPool(baseToken), twapInterval);
    }
```

```solidity
function getSqrtMarkTwapX96(address pool, uint32 twapInterval) internal view returns (uint160) {
        return getSqrtMarkTwapX96From(pool, 0, twapInterval);
    }

    function getSqrtMarkTwapX96From(
        address pool,
        uint32 secondsAgo,
        uint32 twapInterval
    ) internal view returns (uint160) {
        // return the current price as twapInterval is too short/ meaningless
        if (twapInterval < 10) {
            (uint160 sqrtMarkPrice, , , , , , ) = getSlot0(pool);
            return sqrtMarkPrice;
        }
        uint32[] memory secondsAgos = new uint32[](2);

        // solhint-disable-next-line not-rely-on-time
        secondsAgos[0] = secondsAgo + twapInterval;
        secondsAgos[1] = secondsAgo;
        (int56[] memory tickCumulatives, ) = IUniswapV3Pool(pool).observe(secondsAgos);

        // tick(imprecise as it's an integer) to price
        return TickMath.getSqrtRatioAtTick(int24((tickCumulatives[1] - tickCumulatives[0]) / twapInterval));
    }
```

## Impact
The TWAP oracle price can be easily manipulated with a small cost due to the 15 seconds time passed as an argument.

## Code Snippet
The [`getPositionValue()`](https://github.com/sherlock-audit/2023-01-uxd/blob/2f3e8890ba64331be08b690018f93d3b67e82c11/contracts/integrations/perp/PerpDepository.sol#L708) and the [`getMarkPriceTwap()`](https://github.com/sherlock-audit/2023-01-uxd/blob/2f3e8890ba64331be08b690018f93d3b67e82c11/contracts/integrations/perp/PerpDepository.sol#L715)functions.

```solidity
/// @notice Returns the current size of the short position in quote amount.
    /// @return Position size
    function getPositionValue() public view returns (uint256) {
        uint256 markPrice = getMarkPriceTwap(15);
        int256 positionSize = IAccountBalance(clearingHouse.getAccountBalance())
            .getTakerPositionSize(address(this), market);
        return markPrice.mulWadUp(_abs(positionSize));
    }

    function getMarkPriceTwap(uint32 twapInterval)
        public
        view
        returns (uint256)
    {
        IExchange exchange = IExchange(clearingHouse.getExchange());
        uint256 markPrice = exchange
            .getSqrtMarkTwapX96(market, twapInterval)
            .formatSqrtPriceX96ToPriceX96()
            .formatX96ToX10_18();
        return markPrice;
    }
```

## Tool used
Manual Review

## Recommendation
It is recommended to increase the `periodSize` to be greater than 15, 1800 is typically okay.