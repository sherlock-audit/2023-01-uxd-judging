cccz

medium

# TwapInterval in getPositionValue is too small

## Summary
TwapInterval in getPositionValue is too small
## Vulnerability Detail
When calling getMarkPriceTwap in getPositionValue, twapInterval is 15, which is 15 seconds.
```solidity
    function getPositionValue() public view returns (uint256) {
        uint256 markPrice = getMarkPriceTwap(15);
        int256 positionSize = IAccountBalance(clearingHouse.getAccountBalance())
            .getTakerPositionSize(address(this), market);
        return markPrice.mulWadUp(_abs(positionSize));
    }

    function getMarkPriceTwap(uint32 twapInterval)
        public
        view
        returns (uint256)
    {
        IExchange exchange = IExchange(clearingHouse.getExchange());
        uint256 markPrice = exchange
            .getSqrtMarkTwapX96(market, twapInterval)
            .formatSqrtPriceX96ToPriceX96()
            .formatX96ToX10_18();
        return markPrice;
    }
```
When calling the getSqrtMarkTwapX96 function in the perpetual protocol, generally use 0 to get the instant price, or use 15 minutes to get the Twap price.
```solidity
            twapInterval = IClearingHouseConfig(_clearingHouseConfig).getTwapInterval();
            // overflow inspection:
            // 2 ^ 32 = 4,294,967,296 > 100 years = 60 * 60 * 24 * 365 * 100 = 3,153,600,000
            uint32 deltaTimestamp = timestamp.sub(_firstTradedTimestampMap[baseToken]).toUint32();
            twapInterval = twapInterval > deltaTimestamp ? deltaTimestamp : twapInterval;
        }

        uint256 markTwapX96;
        if (marketOpen) {
            markTwapX96 = getSqrtMarkTwapX96(baseToken, twapInterval).formatSqrtPriceX96ToPriceX96();
...
 _twapInterval = 15 minutes;
```
In UniswapV3Broker.getSqrtMarkTwapX96From , use instant price when twapInterval is less than 10 seconds, and 15 seconds interval is too small for Twap price
```solidity
    function getSqrtMarkTwapX96(address pool, uint32 twapInterval) internal view returns (uint160) {
        return getSqrtMarkTwapX96From(pool, 0, twapInterval);
    }

    function getSqrtMarkTwapX96From(
        address pool,
        uint32 secondsAgo,
        uint32 twapInterval
    ) internal view returns (uint160) {
        // return the current price as twapInterval is too short/ meaningless
        if (twapInterval < 10) {
            (uint160 sqrtMarkPrice, , , , , , ) = getSlot0(pool);
            return sqrtMarkPrice;
        }
        uint32[] memory secondsAgos = new uint32[](2);

        // solhint-disable-next-line not-rely-on-time
        secondsAgos[0] = secondsAgo + twapInterval;
        secondsAgos[1] = secondsAgo;
        (int56[] memory tickCumulatives, ) = IUniswapV3Pool(pool).observe(secondsAgos);

        // tick(imprecise as it's an integer) to price
        return TickMath.getSqrtRatioAtTick(int24((tickCumulatives[1] - tickCumulatives[0]) / twapInterval));
    }
```
## Impact
If twapInterval is too small, the Twap price will be easily manipulated
## Code Snippet
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L708-L726
https://github.com/perpetual-protocol/perp-curie-contract/blob/main/contracts/Exchange.sol#L611-L620
https://github.com/perpetual-protocol/perp-curie-contract/blob/main/contracts/ClearingHouseConfig.sol#L44
https://github.com/perpetual-protocol/perp-curie-contract/blob/main/contracts/lib/UniswapV3Broker.sol#L230-L253
## Tool used

Manual Review

## Recommendation
Consider using 15 minutes as twapInterval.

```diff
    function getPositionValue() public view returns (uint256) {
-       uint256 markPrice = getMarkPriceTwap(15);
+       uint256 markPrice = getMarkPriceTwap(15 minutes);
        int256 positionSize = IAccountBalance(clearingHouse.getAccountBalance())
            .getTakerPositionSize(address(this), market);
        return markPrice.mulWadUp(_abs(positionSize));
    }
```