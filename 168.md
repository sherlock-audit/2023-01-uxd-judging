koxuan

medium

# User who rebalances perpDepository can gain excess tokens without taking any risks

## Summary
`rebalance` in `perpDepository` allows user to rebalance PnL of perpDepository. In the `_rebalanceNegativePnlWithSwap`, if the spot swap is lesser than the quoteAmount from the `_placePerpOrder`, the user that called rebalance have to make up the shortfall amount from an account. If the spot swap is more than the quoteAmount, the user is rewarded with the excess tokens. The problem arises when upon inspection that the transfer of tokens from user to perpDepository will only take place if `shortfall` is positive. Therefore, the user can have no quote tokens in their account and the transaction will revert if shortfall is positive and they will receive excess tokens nonetheless if shortfall is negative. 

## Vulnerability Detail
User can rebalance negative PnL of perpDepository by calling `rebalance`. Note that rebalancing positive PnL is disabled according to the source code.

```solidity
    function rebalance(
        uint256 amount,
        uint256 amountOutMinimum,
        uint160 sqrtPriceLimitX96,
        uint24 swapPoolFee,
        int8 polarity,
        address account
    ) external nonReentrant returns (uint256, uint256) {
        if (polarity == -1) {
            return
                _rebalanceNegativePnlWithSwap(
                    amount,
                    amountOutMinimum,
                    sqrtPriceLimitX96,
                    swapPoolFee,
                    account
                );
        } else if (polarity == 1) {
            // disable rebalancing positive PnL
            revert PositivePnlRebalanceDisabled(msg.sender);
            // return _rebalancePositivePnlWithSwap(amount, amountOutMinimum, sqrtPriceLimitX96, swapPoolFee, account);
        } else {
            revert InvalidRebalance(polarity);
        }
    }
```

`_rebalanceNegativePnlWithSwap` will be called, notice how `shortFall` is calculated by  ` quote Amount from _placePerpOrder - spot swap output amount `. If shortfall is positive, the protocol expects account supplied by user to make up the difference by transferring the shortfall tokens over. However, the transfer only takes place if shortFall is positive. A user who has no intention in paying for the rebalance can have no tokens present in account. If shortFall is positive, it reverts and user does not have to pay. If shortFall is negative, the user gains the excess tokens.

```solidity
    function _rebalanceNegativePnlWithSwap(
        uint256 amount,
        uint256 amountOutMinimum,
        uint160 sqrtPriceLimitX96,
        uint24 swapPoolFee,
        address account
    ) private returns (uint256, uint256) {
        uint256 normalizedAmount = amount.fromDecimalToDecimal(
            ERC20(quoteToken).decimals(),
            18
        );
        _checkNegativePnl(normalizedAmount);
        bool isShort = false;
        bool amountIsInput = true;
        (uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder(
            normalizedAmount,
            isShort,
            amountIsInput,
            sqrtPriceLimitX96
        );
        vault.withdraw(assetToken, baseAmount);
        SwapParams memory params = SwapParams({
            tokenIn: assetToken,
            tokenOut: quoteToken,
            amountIn: baseAmount,
            amountOutMinimum: amountOutMinimum,
            sqrtPriceLimitX96: sqrtPriceLimitX96,
            poolFee: swapPoolFee
        });
        uint256 quoteAmountOut = spotSwapper.swapExactInput(params);
        int256 shortFall = int256(
            quoteAmount.fromDecimalToDecimal(18, ERC20(quoteToken).decimals())
        ) - int256(quoteAmountOut);
        if (shortFall > 0) {
            IERC20(quoteToken).transferFrom(
                account,
                address(this),
                uint256(shortFall)
            );
        } else if (shortFall < 0) {
            // we got excess tokens in the spot swap. Send them to the account paying for rebalance
            IERC20(quoteToken).transfer(
                account,
                _abs(shortFall)
            );
        }
        vault.deposit(quoteToken, quoteAmount);


        emit Rebalanced(baseAmount, quoteAmount, shortFall);
        return (baseAmount, quoteAmount);
    }
```

## Impact
User stand to gain excess tokens without taking any risk.

## Code Snippet
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L446-L470
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L478-L528
## Tool used

Manual Review

## Recommendation

Consider doing `transferFrom` a deposit amount from user at the start of the function so that users who have no intention of paying for the rebalance will always revert even if `shortFall` is negative.
