Ruhum

high

# `PerpDepository.rebalance()` can be abused to steal funds

## Summary
An attacker is able to call `PerpDepository.rebalance()` with calldata that allows the tx to be sandwiched with the possibility of someone else covering the shortfall. That will result in a loss of funds for anybody who has PerpDepository approved to spend their USDC as well as an economic incentive for sandwichers to incorporate this in their strategy.


## Vulnerability Detail
The `rebalance()` function is supposed to fix negative PnL. As described [here](https://docs.uxd.fi/uxdprogram-ethereum/other-information/rebalancing-pnl#rebalancing-negative-pnl) it reduces the perp position, withdraws WETH, swaps that to USDC and deposits that into the vault. If the swap results in less USDC than expected (`shortfall > 0`) the difference is paid for by `account`. That's an address provided by the caller. The function expects that address to have the PerpDepository contract approved to spend their USDC tokens, as seen [here](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L443). The function also allows the caller to specify the slippage checks for the swap from WETH->USDC.

If any EOA tries to rebalance the contract, they run into the risk of the following attack to be executed:
1. Alice approves PerpDepository to spend $X$ amount of USDC
2. Bob's bot catches that transaction in the mempool and executes his attack.
3. Bob backruns Alice's approve transaction with a bundle that:
3.1. calls `rebalance()` where the slippage check is set to 0 (allowing Bob to sandwich that trade) and `account` is set to Alice's address
3.2. two transactions that sandwich the `rebalance()` tx causing the PerpDepository contract to receive less USDC than expected. That causes `shortFall` to be larger than zero. The difference is then paid for by Alice.

That allows Bob to steal Alice's funds as he got the sandwich the swap while Alice paid for the short fall.

## Impact
Potentially a loss of funds for anybody that approves the PerpDepository to spend their USDC.

## Code Snippet
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L446
```sol
    /// @notice Rebalance PnL
    /// @param amount The amount in quote token to rebalance.
    /// @param amountOutMinimum The minimum amount of base token to receive in the swap to proceed with rebalancing.
    /// @param polarity the direction of the rebalance. -1 to rebalance negative PnL, +1 for positive PnL
    /// @param sqrtPriceLimitX96 The target price when performing the swap on the spot DEX, and also when placing the perp order.
    /// @param account If there is any shortfall in the swap `account` covers the difference.
    /// For negative PnL, account must have pre-approved this contract to spend quoteToken.
    /// For positive PnL, account must have pre-approved this contract to spend assetToken.
    /// The maximum amount that must be transferred from account = swap fees + slippage.
    function rebalance(
        uint256 amount,
        uint256 amountOutMinimum,
        uint160 sqrtPriceLimitX96,
        uint24 swapPoolFee,
        int8 polarity,
        address account
    ) external nonReentrant returns (uint256, uint256) {
        if (polarity == -1) {
            return
                _rebalanceNegativePnlWithSwap(
                    amount,
                    amountOutMinimum,
                    sqrtPriceLimitX96,
                    swapPoolFee,
                    account
                );
        } else if (polarity == 1) {
            // disable rebalancing positive PnL
            revert PositivePnlRebalanceDisabled(msg.sender);
            // return _rebalancePositivePnlWithSwap(amount, amountOutMinimum, sqrtPriceLimitX96, swapPoolFee, account);
        } else {
            revert InvalidRebalance(polarity);
        }
    }
```
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L478
```sol
    // Collateral price has increased, thus, positionValue > supply. close part of open position
    // close (amount quote) part of position => (baseAmount, quoteAmount)
    // withdraw baseAmount base token.
    // swap base => quote
    // Transfer shortfall from swap from `account`
    // deposit USDC to cover negative PnL
    function _rebalanceNegativePnlWithSwap(
        uint256 amount,
        uint256 amountOutMinimum,
        uint160 sqrtPriceLimitX96,
        uint24 swapPoolFee,
        address account
    ) private returns (uint256, uint256) {
        uint256 normalizedAmount = amount.fromDecimalToDecimal(
            ERC20(quoteToken).decimals(),
            18
        );
        _checkNegativePnl(normalizedAmount);
        bool isShort = false;
        bool amountIsInput = true;
        (uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder(
            normalizedAmount,
            isShort,
            amountIsInput,
            sqrtPriceLimitX96
        );
        vault.withdraw(assetToken, baseAmount);
        SwapParams memory params = SwapParams({
            tokenIn: assetToken,
            tokenOut: quoteToken,
            amountIn: baseAmount,
            amountOutMinimum: amountOutMinimum,
            sqrtPriceLimitX96: sqrtPriceLimitX96,
            poolFee: swapPoolFee
        });
        uint256 quoteAmountOut = spotSwapper.swapExactInput(params);
        int256 shortFall = int256(
            quoteAmount.fromDecimalToDecimal(18, ERC20(quoteToken).decimals())
        ) - int256(quoteAmountOut);
        if (shortFall > 0) {
            IERC20(quoteToken).transferFrom(
                account,
                address(this),
                uint256(shortFall)
            );
        } else if (shortFall < 0) {
            // we got excess tokens in the spot swap. Send them to the account paying for rebalance
            IERC20(quoteToken).transfer(
                account,
                _abs(shortFall)
            );
        }
        vault.deposit(quoteToken, quoteAmount);

        emit Rebalanced(baseAmount, quoteAmount, shortFall);
        return (baseAmount, quoteAmount);
    }
```

## Tool used

Manual Review

## Recommendation
The easiest solution is to force `msg.sender` to cover the shortfall.
