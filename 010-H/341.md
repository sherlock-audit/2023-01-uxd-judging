berndartmueller

high

# Anyone can use the token spending allowance from another address to rebalance negative Perp PnL

## Summary

Leftover token spending allowance from an address that approved the `PerpDepository` to spend `quoteToken`, can be used by anyone else to rebalance the negative Perp PnL and have this `account` address pay for the slippage and swap fees.

## Vulnerability Detail

The `PerpDepository.rebalance` function allows anyone to rebalance (negative) Perp PnL. The internal `._rebalanceNegativePnlWithSwap` function uses Uniswap V3 swaps between the `assetToken` (in) and the `quoteToken` (out). The shortfall of the swap (i.e. slippage and any swap fees) is paid by the provided `account` address.

If an account has the `PerpDepository` contract approved as a spender for the `quoteToken` (e.g. due to previous rebalancing efforts), anyone else can use this address as the `account` parameter for the `PerpDepository.rebalance` function to rebalance the negative Perp PnL and have this `account` address pay for the slippage and swap fees.

## Impact

If an address has a non-zero `quoteToken` token spending allowance for the `PerpDepository` contract, anyone can use this address to rebalance the negative Perp PnL and have this address pay for the shortfall, leading to a loss of funds for this address.

## Code Snippet

[integrations/perp/PerpDepository.rebalance](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L446-L470)

```solidity
function rebalance(
    uint256 amount,
    uint256 amountOutMinimum,
    uint160 sqrtPriceLimitX96,
    uint24 swapPoolFee,
    int8 polarity,
    address account
) external nonReentrant returns (uint256, uint256) {
    if (polarity == -1) {
        return
            _rebalanceNegativePnlWithSwap(
                amount,
                amountOutMinimum,
                sqrtPriceLimitX96,
                swapPoolFee,
                account
            );
    } else if (polarity == 1) {
        // disable rebalancing positive PnL
        revert PositivePnlRebalanceDisabled(msg.sender);
        // return _rebalancePositivePnlWithSwap(amount, amountOutMinimum, sqrtPriceLimitX96, swapPoolFee, account);
    } else {
        revert InvalidRebalance(polarity);
    }
}
```

[integrations/perp/PerpDepository.sol#L512-L516](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L512-L516)

```solidity
function _rebalanceNegativePnlWithSwap(
    uint256 amount,
    uint256 amountOutMinimum,
    uint160 sqrtPriceLimitX96,
    uint24 swapPoolFee,
    address account
) private returns (uint256, uint256) {
    // [...]

    int256 shortFall = int256(
        quoteAmount.fromDecimalToDecimal(18, ERC20(quoteToken).decimals())
    ) - int256(quoteAmountOut);
    if (shortFall > 0) {
        IERC20(quoteToken).transferFrom(
            account,
            address(this),
            uint256(shortFall)
        );
    } else if (shortFall < 0) {
        // we got excess tokens in the spot swap. Send them to the account paying for rebalance
        IERC20(quoteToken).transfer(
            account,
            _abs(shortFall)
        );
    }
    vault.deposit(quoteToken, quoteAmount);

    emit Rebalanced(baseAmount, quoteAmount, shortFall);
    return (baseAmount, quoteAmount);
}
```

## Tool used

Manual Review

## Recommendation

Consider using `msg.sender` as the account instead of allowing any address to be used.

Please note a similar behavior can be observed in the `PerpDepository.rebalanceLite` function. However, in this case, the `account` receives the `assetToken` in return.
