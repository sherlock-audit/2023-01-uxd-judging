DecorativePineapple

high

# An attacker can frontrun the call to the `rebalanceLite` function and steal the amount to be rebalanced by sandwiching the long position that is opened

## Summary
It has been identified that an attacker can frontrun the call to the [`rebalanceLite`](https://github.com/sherlock-audit/2023-01-uxd/blob/2f3e8890ba64331be08b690018f93d3b67e82c11/contracts/integrations/perp/PerpDepository.sol#L597) function, provide a wrong `sqrtPriceLimitX96` and sandwich the long position that's been created.

## Vulnerability Detail
The `PerpDepository` smart contract allows a user to lite rebalance the unrealized PnL by providing the amount to be rebalanced. For this call to succeed the account must have approved this contract to spend the amount (for negative PnL). However, the approval and the call to the [`rebalanceLite`](https://github.com/sherlock-audit/2023-01-uxd/blob/2f3e8890ba64331be08b690018f93d3b67e82c11/contracts/integrations/perp/PerpDepository.sol#L597) function happen in two different function calls. 
On the [Perpetual Protocol Docs](https://support.perp.com/hc/en-us/articles/5257495077145-Opening-and-Closing-Positions) it's mentioned that when a Long Position is opened you are trading quote tokens (e.g. USD) for base tokens (e.g. ETH, BTC, etc.) and when a short position is opened you are trading base tokens (e.g. ETH, BTC, etc.) for quote tokens (e.g. USD). The [`rebalanceLite`](https://github.com/sherlock-audit/2023-01-uxd/blob/2f3e8890ba64331be08b690018f93d3b67e82c11/contracts/integrations/perp/PerpDepository.sol#L597) has the `sqrtPriceLimitX96` argument to protect from frontrun/sandwich attacks during the trade that occurs when a long position is opened. However, the `account` argument isn't validated and anyone can transfer tokens from the `account` to the `PerpDepository` smart contract if the allowance is sufficient.
An attacker can frontrun the call that the user made to the [`rebalanceLite`](https://github.com/sherlock-audit/2023-01-uxd/blob/2f3e8890ba64331be08b690018f93d3b67e82c11/contracts/integrations/perp/PerpDepository.sol#L597) function after the approval, provide the user's address as the `account` argument and also provide a bad `sqrtPriceLimitX96` input that's treated as a slippage protection for the long position. By providing a bad input for the `sqrtPriceLimitX96` argument, the attacker can sandwich the trader that occurs when the long position is created. 

## Impact
Sandwich the trade that occurs when a long position is opened during the [`rebalanceLite`](https://github.com/sherlock-audit/2023-01-uxd/blob/2f3e8890ba64331be08b690018f93d3b67e82c11/contracts/integrations/perp/PerpDepository.sol#L597) function.

## Code Snippet
The [`rebalanceLite`](https://github.com/sherlock-audit/2023-01-uxd/blob/2f3e8890ba64331be08b690018f93d3b67e82c11/contracts/integrations/perp/PerpDepository.sol#L597) function:
```solidity
/// @notice Rebalance unrealized PnL lite.
    /// @dev Lite rebalancing requires the caller to provide the amount to rebalance.
    /// For negative PnL, `account` provides amount in quote token.
    /// For this call to succeed the account must have approved this contract to spend amount (for negative PnL),
    /// or equivalent amount in assetToken (for positive PnL).
    /// @param amount The amount of PnL to rebalance in quote token decimals.
    /// @param polarity The direction of the rebalance. -1 for negative PnL, +1 for positive PnL.
    /// @param sqrtPriceLimitX96 The target price when placing the perp order to resize the delta neutral position.
    /// @param account The account doing the rebalancing. This account must provide the collateral
    /// or quote amount and receives the other side after the swap.
    /// @return (baseAmount, quoteAmount) the amount rebalanced in baseAmount and quoteAmount.
    function rebalanceLite(
        uint256 amount,
        int8 polarity,
        uint160 sqrtPriceLimitX96,
        address account
    ) external nonReentrant returns (uint256, uint256) {
        if (polarity == -1) {
            return
                _rebalanceNegativePnlLite(amount, sqrtPriceLimitX96, account);
        } else if (polarity == 1) {
            // disable rebalancing positive PnL
            revert PositivePnlRebalanceDisabled(msg.sender);
            // return _rebalancePositivePnlLite(amount, sqrtPriceLimitX96, account);
        } else {
            revert InvalidRebalance(polarity);
        }
    }

    function _rebalanceNegativePnlLite(
        uint256 amount,
        uint160 sqrtPriceLimitX96,
        address account
    ) private returns (uint256, uint256) {
        uint256 normalizedAmount = amount.fromDecimalToDecimal(
            ERC20(quoteToken).decimals(),
            18
        );

        _checkNegativePnl(normalizedAmount);
        IERC20(quoteToken).transferFrom(account, address(this), amount);
        IERC20(quoteToken).approve(address(vault), amount);
        vault.deposit(quoteToken, amount);

        bool isShort = false;
        bool amountIsInput = true;
        (uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder(
            normalizedAmount,
            isShort,
            amountIsInput,
            sqrtPriceLimitX96
        );
        vault.withdraw(assetToken, baseAmount);
        IERC20(assetToken).transfer(account, baseAmount);

        emit Rebalanced(baseAmount, quoteAmount, 0);

        return (baseAmount, quoteAmount);
    }
```


## Tool used
Manual Code Review

## Recommendation
It's recommended to remove the `account` argument from the [`rebalanceLite`](https://github.com/sherlock-audit/2023-01-uxd/blob/2f3e8890ba64331be08b690018f93d3b67e82c11/contracts/integrations/perp/PerpDepository.sol#L597) function and replace it with `msg.sender`. By doing this, an attacker can't provide the user's address as the argument of the `rebalanceLite` function and approve himself the amount of tokens for the function to succeed. 