berndartmueller

medium

# ERC-20 tokens with different decimals than 18 break the protocol

## Summary

The `PerpDepository` contract is not prepared to work with ERC-20 tokens as `assetToken` with different decimal precision than 18 decimals. This limits the amount of UXD tokens to mint via this Perp integration due to the Perp collateral deposit caps.

## Vulnerability Detail

The Perp protocol uses a vAMM with virtual tokens (e.g. `vBTC`, `vWETH`, `vUSD`) for trading. `vUSD` is the quote token for all pairs. All virtual tokens share the same decimal precision of **18 decimals**. Opening positions via the `ClearingHouse.openPosition` function expects all token amounts provided as parameters to be in 18 decimal precision and also returns `baseAmount`, `quoteAmount` in 18 decimal precision.

On the contrary, depositing collateral in the Perp vault - `WETH` typically as `assetToken` and `USDC` as `quoteToken`, works with the decimal precision of the token itself (`WETH` has 18 decimals, `USDC` has 6 decimals). The `PerpDepository` contract is aware of the different decimal precisions and converts (normalizes) the amounts to 18 decimals when opening positions with the Perp `clearingHouse`.

However, the authors of the `PerpDepository` integration anticipate only ever using `WETH` as an `assetToken` (or ERC-20 tokens with 18 decimals). The integration contract is not prepared to work with `assetToken` contracts with different decimal precision than 18 decimals. This is not necessarily a problem as of right now, but it limits the amount of UXD tokens to mint via this Perp integration.

Perp currently supports various collateral ERC-20 tokens ([see here for reference](https://support.perp.com/hc/en-us/articles/5257432076569#heading-2)) with specific deposit caps:

| ERC-20 Token | Decimals | Deposit Cap     |
| ------------ | -------- | --------------- |
| USDC         | 6        | 25 million USDC |
| USDT         | 6        | 1 million USDT  |
| ETH / WETH   | 18       | 1,000 ETH       |
| FRAX         | 18       | 40,000 FRAX     |
| OP           | 18       | 700,000 OP      |

The UXD `PerpDepository` integration is currently only focused on `ETH/WETH` (and 18 decimal ERC-20 tokens - `FRAX`, `OP`) and does not work with `USDC` and `USDT`.

As of today (2023-01-15), the Perp protocol's current `WETH` collateral deposit utilization is 452.80/1,000.00 (**45.28%**). This leaves 547,20 `WETH` (= ~845,971 USD) available for depositing. Given the current implementation of the `PerpDepository` contract, the UXD token minting rate on Optimism is limited and requires code updates to support other ERC-20 tokens with different decimal precision.

Thus, I consider Medium severity to be appropriate for this issue.

## Impact

Minting UXD tokens via the Perp integration on Optimism is limited to only `WETH`, `FRAX` and `OP` as `assetToken` (collateral). Using `USDC` and `USDT` as `assetToken` will lead to incorrect amounts being deposited/traded in the Perp protocol or reverted transactions.

## Code Snippet

There are multiple places in the `PerpDepository` contract where the `assetToken` amount is assumed to always be in 18 decimals:

[integrations/perp/PerpDepository.sol#L246](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L246)

```solidity
function deposit(
    address asset,
    uint256 amount
) external onlyController returns (uint256) {
    if (asset == assetToken) {
        _depositAsset(amount);
        (, uint256 quoteAmount) = _openShort(amount); // @audit-info `amount` here is in decimals of `assetToken`
        return quoteAmount;
    } else if (asset == quoteToken) {
        return _processQuoteMint(amount);
    } else {
        revert UnsupportedAsset(asset);
    }
}
```

[integrations/perp/PerpDepository.sol#L498](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L498)

```solidity
function _rebalanceNegativePnlWithSwap(
    uint256 amount,
    uint256 amountOutMinimum,
    uint160 sqrtPriceLimitX96,
    uint24 swapPoolFee,
    address account
) private returns (uint256, uint256) {
    uint256 normalizedAmount = amount.fromDecimalToDecimal(
        ERC20(quoteToken).decimals(),
        18
    );
    _checkNegativePnl(normalizedAmount);
    bool isShort = false;
    bool amountIsInput = true;
    (uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder(
        normalizedAmount,
        isShort,
        amountIsInput,
        sqrtPriceLimitX96
    );
    vault.withdraw(assetToken, baseAmount); // @audit-info also wrong decimals when withdrawing `baseAmount` (= 18 decimals) (it is correct though for WETH, but not for e.g. WBTC = 8 decimals)
    SwapParams memory params = SwapParams({
        tokenIn: assetToken,
        tokenOut: quoteToken,
        amountIn: baseAmount, // @audit-info `baseAmount` is always in 18 decimals, but `assetToken` can have different decimals
        amountOutMinimum: amountOutMinimum,
        sqrtPriceLimitX96: sqrtPriceLimitX96,
        poolFee: swapPoolFee
    });
    uint256 quoteAmountOut = spotSwapper.swapExactInput(params);
    int256 shortFall = int256(
        quoteAmount.fromDecimalToDecimal(18, ERC20(quoteToken).decimals())
    ) - int256(quoteAmountOut);
    if (shortFall > 0) {
        IERC20(quoteToken).transferFrom(
            account,
            address(this),
            uint256(shortFall)
        );
    } else if (shortFall < 0) {
        // we got excess tokens in the spot swap. Send them to the account paying for rebalance
        IERC20(quoteToken).transfer(
            account,
            _abs(shortFall)
        );
    }
    vault.deposit(quoteToken, quoteAmount);

    emit Rebalanced(baseAmount, quoteAmount, shortFall);
    return (baseAmount, quoteAmount);
}
```

[integrations/perp/PerpDepository.sol#L638-L639](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L638-L639)

```solidity
function _rebalanceNegativePnlLite(
    uint256 amount,
    uint160 sqrtPriceLimitX96,
    address account
) private returns (uint256, uint256) {
    uint256 normalizedAmount = amount.fromDecimalToDecimal(
        ERC20(quoteToken).decimals(),
        18
    );

    _checkNegativePnl(normalizedAmount);
    IERC20(quoteToken).transferFrom(account, address(this), amount);
    IERC20(quoteToken).approve(address(vault), amount);
    vault.deposit(quoteToken, amount);

    bool isShort = false;
    bool amountIsInput = true;
    (uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder(
        normalizedAmount,
        isShort,
        amountIsInput,
        sqrtPriceLimitX96
    );
    vault.withdraw(assetToken, baseAmount); // @audit-info `baseAmount` is always in 18 decimals, but `assetToken` can have different decimals
    IERC20(assetToken).transfer(account, baseAmount); // @audit-info here as well

    emit Rebalanced(baseAmount, quoteAmount, 0);

    return (baseAmount, quoteAmount);
}
```

https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol#L337

```solidity
function _redeem(InternalRedeemParams memory redeemParams)
    internal
    returns (uint256)
{
    // [...]

    uint256 amountOut = IDepository(depository).redeem(
        redeemParams.assetToken,
        redeemParams.amountToRedeem
    );

    if (amountOut < redeemParams.minAmountOut) {
        revert CtrlMinNotMet(redeemParams.minAmountOut, amountOut);
    }
    redeemable.burn(msg.sender, redeemParams.amountToRedeem);
    IERC20(redeemParams.assetToken).transfer(redeemParams.intermediary, amountOut); // @audit-info `amountOut` is always in 18 decimal precision (it should be in redeemParams.assetToken.decimals())

    return amountOut;
}
```

## Tool used

Manual Review

## Recommendation

Consider adapting the `PerpDepository` contract to support ERC-20 tokens as `assetToken` with different decimal precision than 18 decimals by converting the amounts appropriately.
