ctf_sec

medium

# Rage trade depository redeem can revert in senior vault beforeWithdraw hook when the utilization rate is high

## Summary

Rage trade depository redeem can revert in senior vault beforeWithdraw hook when the utilization is high

## Vulnerability Detail

In the current implementation of the UXDController, 

when user calls redeem, user calls:

```solidity
/// @notice Redeems a given amount of redeemable token.
/// @param assetToken the token to receive by redeeming.
/// @param redeemAmount The amount to redeemable token being redeemed.
/// @param minAmountOut The min amount of `assetToken` to receive.
/// @param receiver The account to receive assets
function redeem(
	address assetToken,
	uint256 redeemAmount,
	uint256 minAmountOut,
	address receiver
) external nonReentrant returns (uint256) {
	InternalRedeemParams memory rp = InternalRedeemParams({
		assetToken: assetToken,
		amountToRedeem: redeemAmount,
		minAmountOut: minAmountOut,
		intermediary: receiver
	});
	uint256 amountOut = _redeem(rp);
	emit Redeemed(msg.sender, receiver, amountOut);
	return amountOut;
}
```

Which calls:

```solidity
/// @dev internal redeem function
function _redeem(InternalRedeemParams memory redeemParams)
	internal
	returns (uint256)
{
	if (!whitelistedAssets[redeemParams.assetToken]) {
		revert CtrlNotWhitelisted(redeemParams.assetToken);
	}
	if(redeemable.allowance(msg.sender, address(this)) < redeemParams.amountToRedeem) {
		revert CtrlNotApproved(address(redeemable), msg.sender, redeemParams.amountToRedeem);
	}

	address depository = router.findDepositoryForRedeem(
		redeemParams.assetToken,
		redeemParams.amountToRedeem
	);

	uint256 amountOut = IDepository(depository).redeem(
		redeemParams.assetToken, 
		redeemParams.amountToRedeem
	);

	if (amountOut < redeemParams.minAmountOut) {
		revert CtrlMinNotMet(redeemParams.minAmountOut, amountOut);
	}
	redeemable.burn(msg.sender, redeemParams.amountToRedeem);
	IERC20(redeemParams.assetToken).transfer(redeemParams.intermediary, amountOut);

	return amountOut;
}
```

If the depository is rage trade, the redeem on rage trade depository is called.

```solidity
/// @notice Redeem a given amount.
/// @param redeemableAmount The amount to redeem in redeemable.decimals()
/// @return assetAmount The asset amount withdrawn by this redemption
function redeem(address asset, uint256 redeemableAmount)
	external
	onlyController
	returns (uint256)
{
	if (asset != assetToken) {
		revert UnsupportedAsset(asset);
	}
	uint256 assetAmount = _redeemableToAssets(redeemableAmount);
	redeemableUnderManagement -= redeemableAmount;
	netAssetDeposits -= assetAmount;
	uint256 shares = vault.withdraw(
		assetAmount,
		address(controller),
		address(this)
	);
	emit Withdrawn(msg.sender, assetAmount, redeemableAmount, shares);
	return assetAmount;
}
```

the critical call is the vault.withdraw, which calls:

https://github.com/RageTrade/delta-neutral-gmx-vaults/blob/a2107d37b789494454bd4ede7d217d8723474de4/contracts/vaults/DnGmxSeniorVault.sol#L256

```solidity
function withdraw(
	uint256 assets,
	address receiver,
	address owner
) public override(IERC4626, ERC4626Upgradeable) whenNotPaused returns (uint256 shares) {
	// harvesting fees so asset to shares conversion rate is not stale
	dnGmxJuniorVault.harvestFees();
	shares = super.withdraw(assets, receiver, owner);
}
```

Which calls super.withdraw(assets, receiver, owner);

https://github.com/RageTrade/delta-neutral-gmx-vaults/blob/a2107d37b789494454bd4ede7d217d8723474de4/contracts/ERC4626/ERC4626Upgradeable.sol#L109

```solidity
function withdraw(
	uint256 assets,
	address receiver,
	address owner
) public virtual returns (uint256 shares) {
	shares = previewWithdraw(assets); // No need to check for rounding error, previewWithdraw rounds up.

	if (msg.sender != owner) {
		uint256 allowed = allowance(owner, msg.sender); // Saves gas for limited approvals.

		if (allowed != type(uint256).max) _approve(owner, msg.sender, allowed - shares);
	}

	beforeWithdraw(assets, shares, receiver);

	_burn(owner, shares);

	emit Withdraw(msg.sender, receiver, owner, assets, shares);

	IERC20Metadata(asset).safeTransfer(receiver, assets);
}
```

Note the hook beforeWithdraw, which calls:

https://github.com/RageTrade/delta-neutral-gmx-vaults/blob/a2107d37b789494454bd4ede7d217d8723474de4/contracts/vaults/DnGmxSeniorVault.sol#L288

```solidity
function beforeWithdraw(
	uint256 assets,
	uint256,
	address
) internal override {
	/// @dev withdrawal will fail if the utilization goes above maxUtilization value due to a withdrawal
	// totalUsdcBorrowed will reduce when borrower (junior vault) repays
	if (totalUsdcBorrowed() > ((totalAssets() - assets) * maxUtilizationBps) / MAX_BPS)
		revert MaxUtilizationBreached();

	// take out required assets from aave lending pool
	pool.withdraw(address(asset), assets, address(this));
}
```

## Impact

When the borrowing utlization rate is high, the transaction revert in 

```solidity
if (totalUsdcBorrowed() > ((totalAssets() - assets) * maxUtilizationBps) / MAX_BPS)
	revert MaxUtilizationBreached();
```

This can temporialiy lock user's fund and make user not able to redeem, the duraiton can be short, or can be very long.

## Code Snippet

https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/rage-trade/RageDnDepository.sol#L116-L139

## Tool used

Manual Review

## Recommendation

We recommend the project pause the redeem function for rage trade in such an unsual circusmantance. 
