Zarf

high

# no-revert-on-transfer ERC20 tokens not supported

## Summary

Some ERC20 tokens don't throw but just return false when a transfer fails. This can be abused to trick the `mint()` in `UXDController.sol` to mint UXD tokens without providing any collateral.

An example of such a token is *ZRX*: [[Etherscan code](https://etherscan.io/address/0xe41d2489571d322189246dafa5ebde1f4699f498#code#L64)](https://etherscan.io/address/0xe41d2489571d322189246dafa5ebde1f4699f498#code#L64)

## Vulnerability Detail

When minting UXD tokens, collateral is transferred from msg.sender to the depository. In case of a ‘no-revert-on-transfer’ collateral token the `transferFrom()` might return false, which means the token transfer failed (instead of reverting). As the return value is not checked in this `mint()` function, the user might successfully mint UXD tokens without providing any collateral.

```solidity
function mint(
    address assetToken,
    uint256 assetAmount,
    uint256 minAmountOut,
    address receiver
) external nonReentrant returns (uint256) {
    // 1. check that token is approved
    // 2. get clearing house from router
    // 3. transfer tokens from msg.sender to clearing house
    // 4. execute perp tx
    // 6. mint
    IERC20 collateral = IERC20(assetToken);
    address account = msg.sender;
    if(collateral.allowance(account, address(this)) < assetAmount) {
        revert CtrlNotApproved(assetToken, account, assetAmount);
    }

    address depository = router.findDepositoryForDeposit(assetToken, assetAmount);
    collateral.transferFrom(
        account,
        depository,
        assetAmount
    );

    InternalMintParams memory mintParams = InternalMintParams({
        assetToken: assetToken,
        assetAmount: assetAmount,
        minAmountOut: minAmountOut,
        receiver: receiver,
        depository: depository
    });
    return _mint(mintParams);
}
```

## Impact

In case one of the supported collateral tokens is a ‘no-revert-on-transfer’ token, Users might be able to mint UXD tokens without providing any collateral.

## Code Snippet

[https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol#L195-L199](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol#L195-L199)

## Tool used

Manual Review

## Recommendation

Use `safeTransferFrom` instead of `transferFrom` (e.g. by using OZ’s `SafeERC20` contract: [https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/utils/SafeERC20.sol](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/utils/SafeERC20.sol))

Alternatively, wrap the `transferFrom` in a require statement:

```solidity
require(collateral.transferFrom(account, depository, assetAmount));
```