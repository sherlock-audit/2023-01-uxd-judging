ustas

medium

# Unregistering a depository can lead to DoS

## Summary
The `UXDRouter` contract can unregister a depository using `unregisterDepository()`. The problem is that this function does not check if any funds are left in the depository. This can lead to a complete or partial stoppage of the protocol.
## Vulnerability Detail
```solidity
/// @notice Unregisters a previously registered depository
/// @param depository the depository address.
/// @param assetToken the asset to unregister depository for
function unregisterDepository(address depository, address assetToken)
    external
    onlyOwner
{
    bool foundByAsset = false;
    address[] storage byAsset = _depositoriesForAsset[assetToken];
    if (byAsset.length == 0) {
        revert NotExists(assetToken);
    }
    for (uint256 i = 0; i < byAsset.length; i++) {
        if (byAsset[i] == depository) {
            foundByAsset = true;
            byAsset[i] = byAsset[byAsset.length - 1];
            byAsset.pop();
            break;
        }
    }
    if (!foundByAsset) {
        revert NotExists(assetToken);
    }

    emit DepositoryUnregistered(assetToken, depository);
}
```

Depending on how many depositories one `assetToken` has, several variants of events are possible.

In the first scenario, if the number of depositories is 1 when you call `unregisterDepository()` for that depository, all the UXDs that were received on the deposit with the corresponding `assetToken` will no longer be available. If you call `redeem()` on the `UXDController` contract, the transaction will be reverted with an error code `NotExists(assetToken)` because no depository was found, though there is still money on it.
```solidity
address depository = router.findDepositoryForRedeem(
    redeemParams.assetToken,
    redeemParams.amountToRedeem
);
```

In the second variant, if `assetToken` has multiple depositories and we call `unregisterDepository()` for the first of them:
1. In case the next one has funds on it: the `redeem()` function will output them until the depository is depleted. In fact, the number of UXDs will be more than the number of funds in the depository. That is the price for 1 UXD != 1 USD.
2. In case there are no funds on the next one (which is more likely, since `UXDController` always uses the first of the existing depositories) or in case it runs out of funds after the first step. The function `redeem()` will be reverted with the error code `InsufficientAssetDeposits(netAssetDeposits, amount)`.
```solidity
if (amount > netAssetDeposits) {
    revert InsufficientAssetDeposits(netAssetDeposits, amount);
}
```

The only way to restore the protocol and withdraw money from the depository would be to add it back, which makes calling `unregisterDepository()` useless and dangerous for the protocol's operation. 

The vulnerability is possible for any of the presented depository contracts (`PerpDepository` and `RageDnDepository`) since they both do not allow `redeem()` to be used by anyone but the `UXDController`.
## Impact
In the worst case, unsecured UXD (redeemable token), the price of which will not be equal to 1 USD. Breach of the protocol.
At best, reputational losses.
## Code Snippet
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDRouter.sol#L56-L81
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/core/UXDController.sol#L323-L326
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L295-L297

## Tool used
Manual Review, VSCodium, Hardhat 

## Recommendation
Add to `unregisterDepository()` check for `netAssetDeposits() == 0`.
```solidity
error DepositoryIsNotEmpty(address depository, uint256 netAssetDeposits);

/// @notice Unregisters a previously registered depository
/// @param depository the depository address.
/// @param assetToken the asset to unregister depository for
function unregisterDepository(address depository, address assetToken)
    external
    onlyOwner
{
    bool foundByAsset = false;
    address[] storage byAsset = _depositoriesForAsset[assetToken];
    if (byAsset.length == 0) {
        revert NotExists(assetToken);
    }
    for (uint256 i = 0; i < byAsset.length; i++) {
        if (byAsset[i] == depository) {
            foundByAsset = true;
            byAsset[i] = byAsset[byAsset.length - 1];
            byAsset.pop();

            uint256 netAssetDeposits = IDepository(depository)
                .netAssetDeposits();
            if (netAssetDeposits != 0)
                revert DepositoryIsNotEmpty(depository, netAssetDeposits);

            break;
        }
    }
    if (!foundByAsset) {
        revert NotExists(assetToken);
    }

    emit DepositoryUnregistered(assetToken, depository);
}
```