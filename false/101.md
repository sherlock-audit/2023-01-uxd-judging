unforgiven

unlabeled

# [High] attacker can drain or theft any user who gave spending allowance for PerpDepository contract by calling rebalanceLite()

## Summary
Function `rebalanceLite()` Rebalance unrealized PnL and requires the caller to provide the amount to rebalance. For negative PnL, `account` provides amount in quote token. for the call to succeed the `account` must have approved this contract to spend amount (for negative PnL). but if a user gives spending allowance to the contract (for example user gave `unit256.max` unlimited allowance) then attacker can call `rebalanceList()` with `account = user` and also unlimited slippage allowance `sqrtPriceLimitX96 = 0` and then code would use `user` funds and would perform unfair swaps or trades. also attacker can perform sandwich-attack and steal user funds by manipulating the price in the market first then call `rebalanceLite(,user)` to spend user funds in manipulated market and then fixing the price(all in one transaction).

## Vulnerability Detail
This is `rebalanceLite()` and `_rebalanceNegativePnlLite()` code:
```
    function rebalanceLite(
        uint256 amount,
        int8 polarity,
        uint160 sqrtPriceLimitX96,
        address account
    ) external nonReentrant returns (uint256, uint256) {
        if (polarity == -1) {
            return
                _rebalanceNegativePnlLite(amount, sqrtPriceLimitX96, account);
        } else if (polarity == 1) {
            // disable rebalancing positive PnL
            revert PositivePnlRebalanceDisabled(msg.sender);
            // return _rebalancePositivePnlLite(amount, sqrtPriceLimitX96, account);
        } else {
            revert InvalidRebalance(polarity);
        }
    }

    function _rebalanceNegativePnlLite(
        uint256 amount,
        uint160 sqrtPriceLimitX96,
        address account
    ) private returns (uint256, uint256) {
        uint256 normalizedAmount = amount.fromDecimalToDecimal(
            ERC20(quoteToken).decimals(),
            18
        );

        _checkNegativePnl(normalizedAmount);
        IERC20(quoteToken).transferFrom(account, address(this), amount);
        IERC20(quoteToken).approve(address(vault), amount);
        vault.deposit(quoteToken, amount);

        bool isShort = false;
        bool amountIsInput = true;
        (uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder(
            normalizedAmount,
            isShort,
            amountIsInput,
            sqrtPriceLimitX96
        );
        vault.withdraw(assetToken, baseAmount);
        IERC20(assetToken).transfer(account, baseAmount);

        emit Rebalanced(baseAmount, quoteAmount, 0);

        return (baseAmount, quoteAmount);
    }
```
As you can see when PnL is negative attacker can call `rebalanceLite()` and set `account` parameter value as another user's address and contract would try to perform the rebalance action for that user by the slippage specified by attacker. by exploiting this attacker can drain other users funds or steal their funds. if attacker sets `sqrtPriceLimitX96` as zero then it means any slippage is allowed. if market has unfair price attacker can call this function and cause user to lose his funds. or attacker can perform sandwich attack and steal user funds like this:
1. user1 gave `unit256.max` allowance for PerpDepositor contract because he wants to save gas and don't give allowance every time.
2. there is a negative PnL and it's possible to call `rebaalnceLite()`. attacker create a contract which perform this 3 actions:
2.1 attacker's contract would take a flash loan and interact with Perp protocol and create a position to make price to change.
2.2 attacker's contract would call `rebalanceLite(sqrtPriceLimitX96=0,account=user1)` and code would use user1 funds and perform negative rebalancing and in doing so could would call `clearingHouse.openPosition()` with unlimited slippage allowance and because attacker manipulated the market, so user would get a very bad deal.
2.3 attacker's contract would perform the reverse action of the 2.1 (close position) and in doing so attacker would steal user1's funds. and then would return the flash loan.
3. user1 only gave spending allowance and didn't want to call rebalance yet, but other can call it and user user1 funds anytime they want.

so attacker was able to perform griefing or steal funds for any user who gave spending allowance.

## Impact
users would lose funds

## Code Snippet
https://github.com/UXDProtocol/uxd-evm/blob/a3ed1e018e09b9e1f67c970cb234b37683304dfb/contracts/integrations/perp/PerpDepository.sol#L586-L644
https://github.com/UXDProtocol/uxd-evm/blob/a3ed1e018e09b9e1f67c970cb234b37683304dfb/contracts/integrations/perp/PerpDepository.sol#L346-L375

## Tool used
Manual Review

## Recommendation
give users option to specify which addresses can call `rebalanceLite()` by setting `account` as them. (each user should have whitelist of address that himself can manage the list)