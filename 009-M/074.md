cccz

medium

# PerpDepository: totalFeesPaid calculated incorrectly

## Summary
PerpDepository._placePerpOrder uses _calculatePerpOrderFeeAmount function to calculate totalFeesPaid, but clearingHouse charges different fees according to different parameters, which will cause totalFeesPaid to be calculated incorrectly.
## Vulnerability Detail
PerpDepository._placePerpOrder uses _calculatePerpOrderFeeAmount function to calculate totalFeesPaid, _calculatePerpOrderFeeAmount calculates feeAmount based on quoteAmount and feeRatio.
```solidity
    function getExchangeFeeWad() public view returns (uint256) {
        uint256 feeRatio = marketRegistry.getFeeRatio(market);
        return feeRatio.mulWadUp(WAD).divWadDown(HUNDRED_PERCENT);
    }

    /// @notice Returns the PERP fee for a given amount
    /// @dev This is calculated using the fee ratio from the PERP
    /// `MarketRegistry` contract.
    /// @param amount The amount to calculate the fee for
    /// @return The PERP fee for a given amount
    function _calculatePerpOrderFeeAmount(uint256 amount)
        internal
        view
        returns (uint256)
    {
        return amount.mulWadUp(getExchangeFeeWad());
    }
```
The method of charging fees in the perpetual protocol is more complicated. In the OrderBook.replaySwap function, you can see that the way of charging fees in _placePerpOrder is limited to the case where isBaseToQuote is true, and in the _placePerpOrder function, isBaseToQuote may be false.
```solidity
            if (swapState.liquidity > 0) {
                if (params.isBaseToQuote) {
                    step.fee = FullMath.mulDivRoundingUp(step.amountOut, params.exchangeFeeRatio, 1e6);
                }

                fee += step.fee;
```
## Impact
This would make the totalFeesPaid calculation incorrect, returning the wrong protocol state in the getCurrentState function
```solidity
    function getCurrentState() external view returns (DepositoryState memory) {
        return
            DepositoryState({
                netAssetDeposits: netAssetDeposits,
                insuranceDeposited: insuranceDeposited,
                redeemableUnderManagement: redeemableUnderManagement,
                totalFeesPaid: totalFeesPaid,
                redeemableSoftCap: redeemableSoftCap
            });
    }
```
## Code Snippet
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L346-L375
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L794-L819
https://github.com/perpetual-protocol/perp-curie-contract/blob/main/contracts/OrderBook.sol#L334-L341
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L736-L745
## Tool used

Manual Review

## Recommendation
Since ClearingHouse.openPosition does not return fee, consider using ClearingHouse.openPositionFor function to get fee
```solidity
    function openPosition(OpenPositionParams memory params)
        external
        override
        whenNotPaused
        nonReentrant
        checkDeadline(params.deadline)
        returns (uint256 base, uint256 quote)
    {
        // openPosition() is already published, returned types remain the same (without fee)
        (base, quote, ) = _openPositionFor(_msgSender(), params);
        return (base, quote);
    }

```
```solidity
    function openPositionFor(address trader, OpenPositionParams memory params)
        external
        override
        whenNotPaused
        nonReentrant
        checkDeadline(params.deadline)
        returns (
            uint256 base,
            uint256 quote,
            uint256 fee
        )
    {
        // CH_SHNAOPT: Sender Has No Approval to Open Position for Trader
        require(IDelegateApproval(_delegateApproval).canOpenPositionFor(trader, _msgSender()), "CH_SHNAOPT");

        return _openPositionFor(trader, params);
    }
```

https://github.com/perpetual-protocol/perp-curie-contract/blob/main/contracts/ClearingHouse.sol