duc

high

# Inconsistent decimals of the param `amount` of function `_placePerpOrder` in contract `PerpDepository`

## Summary
In contract `PerpDepository`, function `_placePerpOrder` is passed a param `amount` and the contract call this function with inconsistent decimals of it (some calls pass `amount` in decimal 18, some calls pass raw decimal). Then whatever the correct decimal is, contract will call this function with the wrong decimals.
## Vulnerability Detail
* Let's see function `_openShort`, it is called in function `deposit` with `amount` in raw decimal:
```solidity=
function deposit(
    address asset,
    uint256 amount
) external onlyController returns (uint256) {
    if (asset == assetToken) {
        _depositAsset(amount);
        (, uint256 quoteAmount) = _openShort(amount);
        return quoteAmount;
    } else if (asset == quoteToken) {
        return _processQuoteMint(amount);
    } else {
        revert UnsupportedAsset(asset);
    }
}
```
Then in function `_openShort`, it calls function `_placePerpOrder` with param `amount` in raw decimal
```solidity= 
function _openShort(uint256 amount)
    private
    returns (uint256, uint256)
{
    (uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder(
        amount,
        true, // short
        true, // exactInput
        0
    );
    redeemableUnderManagement += quoteAmount;
    _checkSoftCap();
    // emit event here
    return (baseAmount, quoteAmount);
}
```
* function `_rebalanceNegativePnlWithSwap` pass to function `_placePerpOrder` the variable `normalizedAmount`, which is converted to decimal 18
```solidity=
function _rebalanceNegativePnlWithSwap(
    uint256 amount,
    uint256 amountOutMinimum,
    uint160 sqrtPriceLimitX96,
    uint24 swapPoolFee,
    address account
) private returns (uint256, uint256) {
    uint256 normalizedAmount = amount.fromDecimalToDecimal(
        ERC20(quoteToken).decimals(),
        18
    );
    _checkNegativePnl(normalizedAmount);
    bool isShort = false;
    bool amountIsInput = true;
    (uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder(
        normalizedAmount,
        isShort,
        amountIsInput,
        sqrtPriceLimitX96
    );
    
    ...
```
As same as function `_rebalanceNegativePnlLite` (pass amount in decimal 18)

## Impact
When contract use quote tokens and base tokens not in decimal 18 (like USDC, WBTC, ...), some perpetual order will be placed by contract with wrong decimals. Then users and protocol might lose lots of funds.
## Code Snippet
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L313
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L334
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L492
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L632
https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L346-L375

## Tool used

Manual Review

## Recommendation
Find the correct decimal that must be passed to function `clearingHouse.openPosition` and fix all wrong decimal params.

